AppendClass BoxFile
{
	ReloadFile := !(HotloadContext^ ctx,File^ newFile) -> void
	{
		thisData := newFile.GetBlob()
		toks := LoadFile(newFile,thisData,true)

		ReloadFunctions(ctx,newFile,thisData,toks)

		
		changedClasses := AVLSet.{BoxClassBody^}

		ReloadAppends(ctx,newFile,thisData,toks,changedClasses)
		ReloadClasses(ctx,newFile,thisData,toks,changedClasses)

		for it : changedClasses
		{
			it.ReloadClass(ctx)
		}
	}
	ReloadAppends := !(HotloadContext^ ctx, File^ newFile, Blob^ thisData,Token^ toks,AVLSet.{BoxClassBody^} changedClasses) -> void
	{
		cleanedClasses := AVLSet.{void^}

		lst := ParseAppend(toks)

		for iter : lst
		{
			name := iter.Down.Right->{TokenIndent^}.Value

			cls := intGetClass2(name.GetString().Str())

			if cls == null
			{
				iter.EmitError("class not found")
			}else{
				assert(cls is in BoxClassBody)
				cls2 := cls->{BoxClassBody^}
				if cls2 not in cleanedClasses
				{
					cls2.Appends.DeleteIf(@1.0 == this&)
					cleanedClasses.Insert(cls2)
				}
				appendBody := iter.Down.Right.Right
				cls2->{BoxClassBody^}.Appends.Emplace(this&,appendBody,ComputeBracketsHash(thisData,null,appendBody))
				ParsedTokens.Insert(iter)
				changedClasses.Insert(cls2)
			}
		}
	}

	ReloadClasses := !(HotloadContext^ ctx, File^ newFile, Blob^ thisData, Token^ toks,AVLSet.{BoxClassBody^} changedClasses) -> void
	{
		classes := ParseClasses(toks)

		newHashs := AVLMap.{Token^,u32}
		classNames := List.{Tuple.{String^,Token^}}

		for iter : classes
		{
			stVal := iter.Down.Right

			name := String^
			if stVal.Left is TokenString
			{
				name = stVal.Left->{TokenString^}.Value->AsString
			}else if stVal.Left is TokenIndent
			{
				name = stVal.Left->{TokenIndent^}.Value.GetString()
			}else{
				assert(false)
			}
			c := PClassParser(stVal.Right)

			newHashs[iter] = c.ComputeHash(thisData)
			classNames.Push(!{name,iter})
		}

		newClasses := List.{Token^}

		for cls : FileClasses
		{
			if cls.hash in newHashs.Values
				continue

			foundClass := Token^
			className := String^

			for it : classNames
			{
				if it.0^ != cls.Name
					continue
				foundClass = it.1
				className = it.0
			}
			if foundClass != null
			{
				//cls.ReloadClass(ctx,foundClass.Down.Right.Right,thisData)
				//TODO inheritance
				p := PClassParser(foundClass.Down.Right.Right)
				changedClasses.Insert(cls)
				cls.Body = p.bodyToken
				cls.hash = p.ComputeHash(thisData)
			}else{
				newClasses.Push(foundClass)
			}
		}

		for it : newClasses
		{
			name := StringSpan

			if it.Down is TokenIndent
			{
				name = it.Down->{TokenIndent^}.Value
			}else{
				name = GetConstString(it.Down->{TokenString^}.Value.Str())
			}
			d := PClassParser(it.Down.Right.Right)
			AddClass(name,d&)
		}

		//TODO removed classes
	}

	ReloadFunctions := !(HotloadContext^ ctx, File^ newFile, Blob^ thisData, Token^ toks) -> void
	{
		
		fncs := ParseFunctions(toks)

		newHashs := AVLMap.{Token^,u32}
		newFuncNames := List.{Tuple.{String^,Token^}}

		for iter : fncs
		{
			stVal := iter.Down.Right

			c := PFunctionParser(stVal.Right)
			crc := c.ComputeHash(thisData)
			newHashs[iter] = crc

			name := String^
			if stVal.Left is TokenString
			{
				name = stVal.Left->{TokenString^}.Value->AsString
			}else if stVal.Left is TokenIndent
			{
				name = stVal.Left->{TokenIndent^}.Value.GetString()
			}else{
				assert(false)
			}

			newFuncNames.Emplace(name,iter)
		}

		foundFunctions := AVLSet.{Token^}

		for c : Functions
		{
			if c is not in BoxFuncTemplateBody
				continue
			thisFunc := c->{BoxFuncTemplateBody^}
			
			foundFunc := Token^
			for it : newFuncNames
			{
				if it.0^ != thisFunc.Name
					continue
				foundFunc = it.1
				break
			}

			if foundFunc != null
			{
				foundFunctions.Insert(foundFunc)
			}

			if thisFunc.hash in newHashs.Values
				continue

			if foundFunc != null
			{
				//TODO check header types
				d := PFunctionParser(foundFunc.Down.Right.Right)
				thisFunc.ReloadFunctions(ctx,d,thisData)
			} else {
				DeleteFunction(thisFunc)
			}
		}
		for it : newFuncNames
		{
			if foundFunctions.Contain(it.1)
				continue

			d := PFunctionParser(it.1.Down.Right.Right)
			CreateFunc(GetConstStringSpan(it.0.AsStringSpan()),false,d&,null)
		}
		
	}
	DeleteFunction := !(BoxFuncTemplate^ delFunc) -> void
	{
		Functions.DeleteIf(@1 == delFunc)
		CodeSpaces[^].Templates[^].DeleteIf(@1 == delFunc)
	}
}

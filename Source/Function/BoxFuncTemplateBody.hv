
BoxFuncTemplateBody := class extend BoxFuncTemplate
{
	Attrs := FuncAttributes

	inps1 := Token^
	resTyp := Token^
	bdy := Token^
	cns := Token^

	headerCheckHatch := THatch

	Name := StringSpan

	hData := FunctionHeaderInfo

	Constants := List.{FuncTemplateConstant}

	this := !(FuncAttributes atrs,Token^ brackets1,Token^ brackets2,Token^ constToken, Token^ resToken,Token^ bodyIn) -> void
	{
		assert(brackets2 == null)//not implemented
		Attrs = atrs
		inps1 = brackets1
		resTyp = resToken
		cns = constToken
		bdy = bodyIn

		if resTyp == null
			Attrs.IsAutoResult = true

		ths := this&
		SubWork(() ==> [ths] {
			ths.ParseHeader()
			ths.headerCheckHatch.Emit()
		})
		ParentScope = CurrentScope
	}
	GetDefaultValuesCount := virtual !() -> int 
	{
		return hData.DefaultValues.Size() 
	}


	ParentScope := BoxUnit^
	GetParentScope := virtual !() -> BoxUnit^
	{
		return ParentScope
	}

	SetName := virtual !(StringSpan name) -> void
	{
		Name = name
		if CurrentCodeSpace != null
		{
			CurrentCodeSpace.AddTemplate(Name,this&)
		}
	}
	ParseHeader := virtual !() -> void
	{
		if Attrs.IsAutoResult
			hData.IsAutoResult = true
		

		if Attrs.IsLambda
		{
			hData.Types.Emplace(StringSpan(""),GTypeVoidP,false,null->{Token^})
			hData.IsAutoResult = true
		}else{
			if CurrentScope is in BoxClass //TODO allow static functions
			{
				cls := CurrentScope->{BoxClass^}
				hData.Types.Emplace(StringSpan("this"),cls.ItType,true,null->{Token^})
				Attrs.IsMethod = true
			}
		}
		hData.ParseHeader(inps1,resTyp)


		if cns != null
		{
			SyntaxCompress(cns,LexHVInputVars)
			iter := cns.Down
			while iter != null
			{
				if iter.GetValue() != ","
				{
					rc := AVLMap.{StringSpan,BoxUnit^}
					cItm := CheckTemplateConst(null,iter,rc)
					rc.Destroy()
					Constants.Emplace(iter,cItm)
				}
				iter = iter.Right
			}
		}
	}
	ToFuncType := virtual !(CheckExeDownList objs,List.{BoxExeConstObj^} consts) -> TypeFunc^
	{
		headerCheckHatch.Await()

		minArgs := hData.Types.Size()
		maxArgs := hData.Types.Size()
		
		minArgs -= GetDefaultValuesCount()

		if hData.IsVariadic
			maxArgs = 2000000 //INT_MAX
		
		if objs.Size() < minArgs or objs.Size() > maxArgs
			return null
		if consts.Size() != Constants.Size()
			return null

		fInTypes := Type^[100]
		isRefs := bool[100]

		datas := HybridQueue.{Tuple.{BoxExeObj^,Type^,bool},10}

		for it,i  : objs
		{
			if it? is BoxArrayHolder and i < hData.Types.Size() and it->{BoxArrayHolder^}.GetArraySize() == 0
			{
				if hData.Types[i].ItType == null
					return null
			}
		}


		addObjType := (int i) ==> {
			if objs[i] is BoxArrayHolder
			{
				ar := objs[i]->{BoxArrayHolder^}

				if ar.GetArraySize() == 0
				{
					assert(hData.Types[i].ItType != null)
					datas.Emplace(objs[i],hData.Types[i].ItType,hData.Types[i].IsRef)
				}else{
					datas.Emplace(objs[i],ar.GetArrayType(),true)
				}//TODO better exchange
			}else{
				datas.Emplace(objs[i],objs[i].GetType(),objs[i].IsMem())
			}
		}

		if objs.Size() > hData.Types.Size()
		{
			for i : objs.Size()
			{
				addObjType(i)
			}
		}else{
			for i : hData.Types.Size()
			{
				if i < objs.Size()
				{
					addObjType(i)
				}else{
					datas.Emplace(null->{BoxExeObj^},hData.Types[i].ItType,hData.Types[i].IsRef)
				}
			}
		}

		resConsts := AVLMap.{StringSpan,BoxUnit^}
		defer resConsts.Destroy()
		return checkTypes(datas,consts,resConsts)
	}
	
	CreateFunc := virtual !(TypeFunc^ tp, List.{BoxExeConstObj^} consts) -> BoxFunc^
	{
		headerCheckHatch.Await()

		datas := HybridQueue.{Tuple.{BoxExeObj^,Type^,bool},10}

		for i : tp.Params.Size()
		{
			datas.Emplace(null->{BoxExeObj^},tp.Params[i].ItType,tp.Params[i].IsRef)
		}
		resConsts := AVLMap.{StringSpan,BoxUnit^}
		checkTypes(datas,consts,resConsts)


		names := HybridQueue.{StringSpan,10}
		names.Push(hData.Types[^].Name)
		newFunc := new BoxFuncBody(tp,Attrs,names,bdy)

		newFunc.VariadicName = hData.VariadicName
		newFunc.SetPrintName(Name)
		newFunc.DefaultValues[ind] = hData.DefaultValues[^ind]
		newFunc.ParentScope = ParentScope

		for it,ind : resConsts
		{
			if it is ConstType
			{
				newFunc.ConstVars.Push(new TypeVar(ind,it->{ConstType^}.Value))
			}else{
				newFunc.ConstVars.Push(new ConstVar(ind,it->{BoxExeConstObj^}))
			}
		}

		return newFunc
	}
	checkTypes := !(HybridQueue.{Tuple.{BoxExeObj^,Type^,bool},10} objs, List.{BoxExeConstObj^} consts, AVLMap.{StringSpan,BoxUnit^} resCnsts) -> TypeFunc^
	{
		//TODO , multi round check

		fTyps := Type^[100]
		iRef := bool[100]

		for i : consts.Size()
		{
			if Constants[i].Value? != consts[i]
				return null
			outConst := CheckTemplateConst(consts[i],Constants[i].ItToken,resCnsts)

			if Constants[i].Value? != outConst?
				return null
		}

		for i : objs.Size()
		{
			if i >= hData.Types.Size()
			{
				assert(hData.IsVariadic)
				fTyps[i] = objs[i].1
			}else{
				iRef[i] = hData.Types[i].IsRef
				if hData.Types[i].ItType != null
				{
					fTyps[i] = hData.Types[i].ItType
				}else if hData.Types[i].ItToken != null
				{
					fTyps[i] = checkType(objs[i].0,objs[i].1,hData.Types[i].ItToken,resCnsts)
				}
			}
		}


		for i : objs.Size()
		{
			if fTyps[i] == null
				return null
			if IsComplexType(fTyps[i])
				iRef[i] = true
		}
		resTypeT := hData.ResultType.ItType

		rTokenTree := hData.ResultType.ItToken
		if rTokenTree?.GetValue() == "ref"
		{
			rTokenTree = rTokenTree.Right
		}

		resTypeT = checkType(null,null,rTokenTree,resCnsts)
		if resTypeT == null and rTokenTree != null
			rTokenTree.EmitError("Can not get result value")
		return GetFuncType(objs.Size(),fTyps[0]&,iRef[0]&,resTypeT,hData.ResultType.IsRef,hData.IsVargs)
	}
	checkType := !(BoxExeObj^ obj,Type^ tp,Token^ tk, AVLMap.{StringSpan,BoxUnit^} resCnsts) -> Type^
	{
		return CheckTemplateType(obj,tp,tk,resCnsts)
	}

	IsOneFunc := virtual !() -> bool
	{
		headerCheckHatch.Await()

		if not hData.IsOneFunc()
			return false
		if Constants[^].Value == null
			return false
		return true
	}
	GetOneFunc := virtual !() -> BoxFunc^
	{
		headerCheckHatch.Await()
			
		resFuncTyp := hData.CreateFuncType()
		cnsts := List.{BoxExeConstObj^}
		return CreateFunc(resFuncTyp,cnsts)
	}

}

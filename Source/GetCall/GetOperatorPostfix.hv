TryGetPostfixOperator := !(Token^ itm) -> BoxExeObj^
{
	if itm == null return null
	if itm.Down == null return null
	if itm.Down.Right == null return null
	if itm.Down.Right.Right != null return null

	oper := itm.Down.Right.GetValue()

	if oper == "?"
	{
		dwn := itm.Down
		if dwn.Down?.Right?.GetValue() == "[]" and dwn.Down.Right.Right == null
		{
			cl := TryGetExeCall(dwn.Down)
			assert(cl != null)

			objs := CheckExeDownList
			objs.Push(cl)
			namedInputs := AVLMap.{Str,BoxExeObj^}
			if not CheckFuncCallBrackets(dwn.Down.Right,objs,namedInputs)
				return null

			cnsts := List.{BoxExeConstObj^}

			cl2 := CreateFuncCallOperator(StringSpan("[]?"),objs,cnsts)
			if cl2 == null
			{
				cl2 = CreateFuncCallOperator(StringSpan("[]"),objs,cnsts)
				if cl2 == null
				{
					CurrentWork?.SetErrorMessage(dwn.MakeError("Could not create function call []? or []\n"))
					return null
				}
			}
			if cl2.IsMem()
			{
				cl2 = new UnrefFuncCall(cl2)
				cl2 = new BoxQuestionPointer(cl2)
				cl2 = new PtrToRef(cl2)
			}
			if cl2.GetType() is in TypePoint
			{
				cl2 = new BoxQuestionPointer(cl2)
			}
			NeedGuardLocalVar = true
			return cl2
		}

		cl := TryGetExeCall(itm.Down)

		if cl == null
			return null

		clType := cl.GetType()
		//TODO variable, if test? { test now is T vriable, not a wrapper of T

		if clType is in TypeClass
		{
			Objs := CheckExeDownList
			Objs.Push(cl)
			consts := List.{BoxExeConstObj^}
			cl2 := CreateFuncCallOperator(StringSpan("?"),Objs,consts)
			if cl2 != null
			{
				if cl2.IsMem()
				{
					cl2 = new UnrefFuncCall(cl2)
					cl2 = new BoxQuestionPointer(cl2)
					cl2 = new PtrToRef(cl2)
				}
				if cl2.GetType() is in TypePoint
				{
					cl2 = new BoxQuestionPointer(cl2)
				}
				NeedGuardLocalVar = true
				return cl2
			}
		}

		if not (cl.GetType() is in TypePoint)
		{
			CurrentWork?.SetErrorMessage(itm.Down.Right.MakeError("Left of ? operator must be pointer"))
			return null
		}
		NeedGuardLocalVar = true
		return new BoxQuestionPointer(cl)
	}
	if oper == "[]"
	{
		Objs := CheckExeDownList
		
		dObj := TryGetExeCall(itm.Down)
		if dObj? is BoxArrayHolder
		{
			dObj = dObj->{BoxArrayHolder^}.CreateDefaultUse()
		}
		if dObj == null
			return null
		if dObj.GetType() == null
			return null
		if dObj.GetType() is TypeArrayView
			dObj = new PtrToRef(new PtrExchange(dObj,dObj.GetType()->{TypeArrayView^}.viewClassType.GetPoint()))
		Objs.Push(dObj)
		SyntaxCompress(itm.Down.Right,PriorityData)
		namedInputs := AVLMap.{Str,BoxExeObj^} //TODO use varaible
		if not CheckFuncCallBrackets(itm.Down.Right,Objs,namedInputs)
			return null

		cnsts := List.{BoxExeConstObj^}
		return CreateFuncCallOperator(StringSpan("[]"),Objs,cnsts)?

		if Objs.Size() >= 1 and Objs[0].GetType() == GTypeStr
		{
			asSt := strAsString(Objs[0])
			asSt = new PtrToRef(asSt)
			Objs[0] = asSt
			return CreateFuncCallOperator(StringSpan("[]"),Objs,cnsts)?
			Objs[0] = dObj
		}

	}
	if oper == "--" or oper == "++"
	{
		Objs2 := CheckExeDownList
		dObj := TryGetExeCall(itm.Down)
		if dObj == null
			return null
		Objs2.Push(dObj)

		return CreateFuncCall(oper,Objs2)
	}
	return null
}

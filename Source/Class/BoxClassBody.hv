
TryParseClass := !(Token^ itm) -> BoxUnit^
{
	if itm == null or itm.Down == null
		return null
	if  itm.Down.GetValue() != "class" and itm.Down.GetValue() != "packed_class"
		return null
	

	parents := HybridQueue.{Tuple.{Token^,InheritanceType},2}
	constValue := Token^

	isPacked := bool
	if itm.Down.GetValue() == "packed_class"
		isPacked = true

	iter := itm.Down.Right

	if iter.GetValue() == "."
	{
		constValue = iter.Right
		iter = constValue.Right
	}

	while iter.GetValue() in !["extend","#Reserve"]
	{
		iter = iter.Right

		inh := InheritanceType
		switch iter.Left.GetValue()
		{
			case "extend" inh = "extend"
			case "#Reserve" inh = "reserve"
		}

		parents.Emplace(iter,inh)
		iter = iter.Right

		while iter.GetValue() == ","
		{
			iter = iter.Right
			parents.Emplace(iter,inh)
			iter = iter.Right
		}
		assert(iter.GetValue() == "{}")
	}
	bdy := iter

	if bdy == null
		return null

	if constValue != null
		return new BoxClassTemplateBody(isPacked,parents,bdy,constValue)
	return new BoxClassBody(isPacked,parents,bdy)
}


BoxClassBody := class extend BoxClassPoison
{
	Body := Token^
	ParentTokens := HybridQueue.{Tuple.{Token^,InheritanceType},5}

	VParentState := THatch
	VTableState := THatch
	FieldsState := THatch

	NamedConstants := AVLMap.{StringSpan,BoxUnit^}
	NamedConstantsVars := List.{BoxUnit^}

	ComputeConsts := !() -> void
	{
		for it,ind : NamedConstants
		{
			if it is ConstType
			{
				NamedConstantsVars.Push(new TypeVar(ind,it->{ConstType^}.Value))
			}else{
				assert(it is in BoxExeConstObj)
				NamedConstantsVars.Push(new ConstVar(ind,it->{BoxExeConstObj^}))
			}
		}
	}
	PutItConstants := virtual !() -> void
	{
		for it : NamedConstantsVars
		{
			UnitStack.PushFront(it)
		}
	}

	ParentScope := BoxUnit^
	GetParentScope := virtual !() -> BoxUnit^ { return ParentScope }

	this := !(bool isPacked,HybridQueue.{Tuple.{Token^,InheritanceType},2} parents,Token^ bdy) -> void
	{
		ItName = "anon" //todo
		ItId = GetNewId()
		ItType = new TypeClass(this&)
		
		ParentTokens.Push(parents[^])

		ParentScope = CurrentScope
		itFile := GetFileScope()

		IsPacked = isPacked
		Body = bdy
		itFile.AllClasses.Push(this&)

		classFile = CurrentFile
	}

	startedParse := bool
	classFile := BoxFile^
	StartParseBody := virtual !() -> void
	{
		if startedParse
			return void
		startedParse = true

		GClassesInProgress.Push(this&)
		cls := this&
		TSpawnTask(() ==> [cls] { //todo pass CurrentFile directly?
			CurrentFile = cls.classFile
			cls.ParseBody()	
		})
	}
	AwaitTypeState := virtual !() -> void
	{
		StartParseBody()
		FieldsState.Await()
	}
	AwaitReadyState := virtual !() -> void
	{
		StartParseBody()
		AwaitTypeState()
	}
	ParseBody := virtual !() -> void
	{
		GParsedAllFiles.Await()

		ScopePutConstants()

		buildCtx := ClassBuildContext

		for i : ParentTokens.Size()
		{
			inhType := CheckTemplateType(null,ParentTokens[i].0,NamedConstants)
			if inhType == null
				ParentTokens[i].0.EmitError("can not inherit unknown class")
			if not (inhType is TypeClass)
				ParentTokens[i].0.EmitError("parent type must be class")
			asClsParent := inhType->{TypeClass^}
			asClsParent.ToClass.StartParseBody()
			asClsParent.ToClass.AwaitParentStep()

			buildCtx.All.Emplace(asClsParent.ToClass,ParentTokens[i].1)
		}
		
		for it : buildCtx.All
		{
			if it.1 == "reserve"
				continue

			for ress : it.0.Reserved
			{
				buildCtx.ReservedParents.Push(ress)
			}
		}
		for it : buildCtx.All
		{
			itReserved := false
			for inReserv : buildCtx.ReservedParents
			{
				if inReserv == it.0
					itReserved = true
			}
			if itReserved
			{
				buildCtx.FillReserve.Push(it.0)
			}else{
				buildCtx.MemParents.Push(it.0)
				if not (it.1 == "reserve")
					buildCtx.FieldsParents.Push(it.0)
			}
		}

		for it : buildCtx.MemParents
		{
			Parents.Push(it)
		}

		for it : buildCtx.All
		{
			if not (it.1 == "reserve")
				continue
			buildCtx.ReservedParents.Push(it.0)
		}

		for it : buildCtx.ReservedParents
		{
			isMissed := true
			for fld : buildCtx.FillReserve
			{
				if fld == it
					isMissed = false
			}
			if isMissed
			{
				buildCtx.UnfilledReserve.Push(it)
			}
		}
		Reserved.Push(buildCtx.UnfilledReserve[^])

		for it : buildCtx.FillReserve
		{
			ApplyedReserve.Push(it)	
		}

		PrimeParentStep()
		VParentState.Emit()

		CurrentScope = this&

		funcs := List.{Tuple.{StringSpan,BoxFunc^}}

		checkClassBody(Body,funcs)
		for it : Appends
		{
			checkClassBody(it,funcs)
		}
		
		if buildCtx.MemParents.Size() != 0
		{
			buildCtx.MemParents[0].AwaitVTableStep()
			for it,i : buildCtx.MemParents[0].VTable
			{
				VTable.Push(it)
			}
		}

		populatePoisons(Parents[^])

		PrepareVTable(buildCtx)
		CreateVTable(buildCtx)

		emitTest := static AVLSet.{void^}

		VTableState.Emit()

		InheritStep(buildCtx)
		FieldsState.Emit()

	}

	checkClassBody := !(Token^ bodyVal,@TT funcs) -> void
	{
		SyntaxCompress(bodyVal,LexHVFileRoot)
		iter := bodyVal.Down

		while iter != null
		{
			//if iter.Down? is TokenIndent and iter.Down->{TokenIndent^}.Value == "#Virtualize"
			//{
			//	dwns := CheckExeDownList

			//	itr2 := iter.Down.Right.Down

			//	virtualName := itr2->{TokenString^}.Value

			//	itr2 = itr2.Right.Right

			//	funcName := itr2->{TokenString^}.Value

			//	itr2 = itr2.Right

			//	while itr2 != null
			//	{
			//		if itr2.GetValue() == ","
			//		{
			//			itr2 = itr2.Right
			//			continue
			//		}
			//		tp := ParseType(itr2)

			//		assert(tp != null) //TODO emit error

			//		dwns.Push(new BlankObjectExe(tp,false)) //TODO also refs, clean memory

			//		itr2 = itr2.Right
			//	}

			//	vrFunc := CreateFuncCall(funcName,dwns)
			//	printf("test %s %s %p\n",virtualName,funcName,vrFunc)

			//	iter = iter.Right
			//	continue
			//}

			if iter.Down?.Right?.GetValue() == ":="
			{
				assert(iter.Down is TokenIndent or iter.Down is TokenString) // left of := can only be name by syntax creation
				name := StringSpan
				if iter.Down is TokenIndent
				{
					name = iter.Down->{TokenIndent^}.Value
				}else{
					name = iter.Down->{TokenString^}.Value
				}

				itm := iter.Down.Right.Right

				asType := ParseType(itm)
				if asType != null
				{
					newField := new FieldParam(name,asType)
					Fields.Push(newField)
				}else
				{

					asFunc := BoxUnit^
					isPoison := false
					isVirtual := false

					while true
					{
						switch itm.GetValue()
						{
							case "poison"
								isPoison = true
								itm = itm.Right
							case "virtual"
								isVirtual = true
								itm = itm.Right
							case void
								break
						}
					}

					fncParser := PFunctionParser(itm)
					fncUsed := false

					if fncParser.IsFound()
					{
						if itm.Line?.Attributes.Size() != 0 and SubStrContain(itm.Line.Attributes,"TemplateResist")  and not isPoison and TemplateParent != null
						{
							
							prepData := ref TemplateParent->{BoxClassTemplateBody^}.ResistFunctions[itm.Line][name]

							if prepData == null
							{
								//TLock(TemplateParent)
								//defer TUnlock(TemplateParent)
								if prepData == null
								{
									asFunc = fncParser.CreateObject()
									if asFunc is in BoxFuncTemplate and asFunc->{BoxFuncTemplate^}.IsOneFunc()
									{
										asFunc = asFunc->{BoxFuncTemplate^}.GetOneFunc()
									}

									assert(asFunc != null and asFunc is in BoxFuncBody)
									prepData = asFunc
									asFunc->{BoxFuncBody^}.SetName(name)
								}
							}

							probFunc := fncParser.CreateObject()
							defer delete probFunc

							assert(probFunc != null and probFunc is BoxFuncTemplateBody)
							asTmplt := probFunc->{BoxFuncTemplateBody^}
							assert(asTmplt.IsOneFunc())
							asFunc = new ResistFunc(prepData->{BoxFunc^},asTmplt.hData.CreateFuncType())

						}else{
							asFunc = fncParser.CreateObject()
						}
					}

					if fncUsed
					{
					}else if asFunc != null
					{
						if isPoison
						{
							assert(asFunc is in BoxFuncTemplateBody)
							asTmpl := asFunc->{BoxFuncTemplateBody^}
							PoisonFunctions[name] = !{itm,isVirtual}
						}
						if asFunc is in BoxFuncTemplate and asFunc->{BoxFuncTemplate^}.IsOneFunc()
						{
							asFunc = asFunc->{BoxFuncTemplate^}.GetOneFunc()
						}
						if asFunc is in BoxFunc
						{
							if asFunc is in BoxFuncBody
								asFunc->{BoxFuncBody^}.ParentScope = this&

							f := asFunc->{BoxFunc^}
							f.SetName(name)
							f.Work()

							if isVirtual
							{
								PreVTableFuncs[name].Push(f)
							}else{
								DefaultSpace.Methods[name].Push(f)
							}
						}else {
							if isPoison
								itm.EmitError("poison function can not be template")
							tmpl := asFunc->{BoxFuncTemplate^}
							tmpl.SetName(name)
							DefaultSpace.MethodTemplates[name].Push(tmpl)
						}
					}else
					{
						if itm.Down?.GetValue() == "fake"
						{
							CheckFakeFields(iter)
						}else{
							if itm.GetValue() == "default"
							{
								InternalsHatch.Await()

								setFunc := BoxFunc^
								cnsts := List.{BoxExeConstObj^}
								switch name
								{
									case "="
										setFunc = GDefautlSetTemplate.CreateFunc(GetFuncType(2,![ItType,ItType],![true,true],GTypeVoid,false,false),cnsts)
									case "<=>"
										setFunc = GDefautlCompareTemplate.CreateFunc(GetFuncType(2,![ItType,ItType],![true,true],GTypeInt,false,false),cnsts)
									case void
										itm.EmitError("unknown default operator")
								}
								assert(setFunc != null)
								DefaultSpace.Methods[name].Push(setFunc)
							}else {
								// typ2 := ParseType(itm.Down.Right)
								// if typ2 == null
								// 	itm.EmitError("Unknown object")
								exeObj := TryGetExeCall(itm)
								if exeObj == null
									itm.EmitError("Unknown object")	
								if exeObj is in BoxExeConstObj and isVirtual
								{
									newVal := new VTableVar(name,exeObj->{BoxExeConstObj^})
									PreVTableVars[name] = newVal
								}else{
									itm.EmitError("Unknown object")
								}
							}
						}
					}
				}
			}
			iter = iter.Right
		}

	}
	CheckFakeFields := !(Token^ itm) -> void
	{
		if not (itm.Down is TokenIndent)
			itm.EmitError("fake field must be named by simple word")

		fieldName := itm.Down->{TokenIndent^}.Value
		
		skob := itm.Down.Right.Right.Down.Right
		SyntaxCompress(skob,LexHVFileRoot)

		itr := skob.Down

		thisField := FieldSpaces[fieldName]&

		thisField.itClass = this&

		while itr != null
		{
			if itr.Down == null or itr.Down.Right == null or itr.Down.Right.GetValue() != ":="
				itr.EmitError("Fake field can only contain functions")
			name := itr.Down->{TokenIndent^}.Value //todo , str type

			asFunc := TryParseFunction(itr.Down.Right.Right)
			if asFunc != null
			{
				if asFunc is in BoxFuncTemplate and asFunc->{BoxFuncTemplate^}.IsOneFunc()
				{
					asFunc = asFunc->{BoxFuncTemplate^}.GetOneFunc()
				}
				if asFunc is in BoxFunc
				{
					if asFunc is in BoxFuncBody
						asFunc->{BoxFuncBody^}.ParentScope = this&

					f := asFunc->{BoxFunc^}
					thisField.Methods[name].Push(f)
					f.SetName(name)
					f.Work()
				}else {
					tmpl := asFunc->{BoxFuncTemplate^}
					tmpl.SetName(name)
					thisField.MethodTemplates[name].Push(tmpl)
				}
			}else
			{
			}
			itr = itr.Right
		}
	}
	AwaitParentStep := virtual !() -> void { StartParseBody() VParentState.Await() }
	AwaitVTableStep := virtual !() -> void { StartParseBody() VTableState.Await() }
}

GetVecXTypeSize := !(Type^ itY) -> int
{
	if itY is not TypeVector
		return 0
	return itY->{TypeVector^}.vecElementCount
}

GetVecTypeX := !(Type^ base, int Siz) -> Type^
{
	if base == GTypeFloat
	{
		switch Siz
		{
			case 4 return GTypeVec4f
			case 3 return GTypeVec3f
			case 2 return GTypeVec2f
		}
	}
	if base == GTypeInt
	{
		switch Siz
		{
			case 4 return TypeTable[23]
			case 3 return TypeTable[24]
			case 2 return TypeTable[25]
		}
	}
}

BuiltInGetVecTemplate := class extend BoxFuncTemplate
{
	this := !() -> void
	{
	}
	ToFuncType := virtual !(Vector.{BoxExeObj^} objs, Vector.{BoxExeConstObj^} consts) -> TypeFunc^
	{
		if objs.Size() != 1 or consts.Size() != 1
			return null
		
		a := objs[0].GetType()
		vecSize := GetVecXTypeSize(a)
		if vecSize == 0
			return null
		
		strVal := consts[0]->{BoxExeString^}
		if strVal is not BoxExeString
			return null
		switch strVal.Value^
		{
			case "x"
				//always here
			case "y"
				//always here
			case "z"
				if vecSize < 3
					return null
			case "w"
				if vecSize < 4 
					return null
			case void
				if strVal.Value.Size() > 4
					return null
				if strVal.Value.Size() <= 0
					return null
				
				for i : strVal.Value.Size()
				{
					c := strVal.Value^[i]

					if not (c in "xyzw0")
						return null
					if vecSize < 4 and c == 'w'
						return null
					if vecSize < 3 and c == 'z'
						return null
				}
				resType := Type^
				if a == GTypeQuantf and strVal.Value.Size() == 4
				{
					resType = GTypeQuantf
				}else{
					resType = GetVecTypeX(a.Base,strVal.Value.Size())
				}

				return GetFuncType(1,![a],![true],resType,false,false)
		}


		inpMem := objs[0].IsMem()
		return GetFuncType(1,![a],![inpMem],a.Base,inpMem,false)
	}
	CreateFunc := virtual !(TypeFunc^ tp, Vector.{BoxExeConstObj^} consts) -> BoxFunc^
	{
		str := StringSpan(consts[0]->{BoxExeString^}.Value.Str())
		vecInd := 0
		simpleGet := true
		switch str
		{
			case "x"
				vecInd = 0
			case "y"
				vecInd = 1
			case "z"
				vecInd = 2
			case "w"
				vecInd = 3
			case void
				simpleGet = false
		}
		if simpleGet
		{
			if tp.Params[0].IsRef
			{
				return new BuiltInVecElementRef(tp,vecInd)
			}else{
				return new BuiltInVecElementValue(tp,vecInd)
			}
		}else{
			inds := int[4]
			for i : str.Size()
			{
				if str[i] == 'x' inds[i] = 0
				if str[i] == 'y' inds[i] = 1
				if str[i] == 'z' inds[i] = 2
				if str[i] == 'w' inds[i] = 3
				if str[i] == '0' inds[i] = -1
			}
			return new BuiltInVecShuffle(tp,inds[0]&,str.Size())
		}
		assert(false)
	}
}
BuiltInVecElementRef := class extend BoxFuncCommon
{
	vecInd := int
	this := !(TypeFunc^ tp,int ind) -> void
	{
		SetType(tp)
		vecInd = ind
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream
		typ := lst[0].GetType()
		lst[0].PrintPointPre(ctx)

		f^ << "%T" << resId << " = getelementptr "
		typ.PrintType(ctx)
		f^ << " , "
		typ.PrintType(ctx)
		f^ << "* "
		lst[0].PrintPointUse(ctx)
		f^ << " , i32 0, i32 " << vecInd
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
}
BuiltInVecElementValue := class extend BoxFuncCommon
{
	vecInd := int
	this := !(TypeFunc^ tp,int ind) -> void
	{
		SetType(tp)
		vecInd = ind
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream
		typ := lst[0].GetType()
		lst[0].PrintPre(ctx)

		f^ << "%T" << resId << " = extractelement "
		typ.PrintType(ctx)
		f^ << " "
		lst[0].PrintUse(ctx)
		f^ << " , i32 " << vecInd
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
}
BuiltInVecShuffle := class extend BoxFuncCommon
{
	indexes := int[4]
	vecSize := int
	this := !(TypeFunc^ tp,int^ inds,int vs) -> void
	{
		SetType(tp)
		for i : vs
		{
			indexes[i] = inds[i]
		}
		vecSize = vs
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream
		typ := lst[0].GetType()

		lst[0].PrintPre(ctx)

		f^ << "%T" << resId << " = shufflevector "
		typ.PrintType(ctx)
		f^ << " "
		lst[0].PrintUse(ctx)
		f^ << " , "
		typ.PrintType(ctx)
		f^ << " zeroinitializer ," 
		f^ << "<" << vecSize << " x i32 > <"
		for i : vecSize
		{
			if i != 0
				f^ << ","
			if indexes[i] == -1 {
				f^ << "i32 " << vecSize
			}else{
				f^ << "i32 " << indexes[i]
			}
		}
		f^ << ">"
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
}

BuiltInVecConstrTemplate := class extend BoxFuncTemplate
{
	ToFuncType := virtual !(Vector.{BoxExeObj^} objs, Vector.{BoxExeConstObj^} consts) -> TypeFunc^
	{
		if objs.Size() > 5 or objs.Size() < 2
			return null
		
		a := objs[0].GetType()
		vecSize := GetVecXTypeSize(a)
		if vecSize == 0
			return null
		inpts := objs.Size() - 1
		if inpts > vecSize
			return null
		
		typs := Type^[6]
		for i : objs.Size()
		{
			typs[i] = objs[i].GetType()
		}
		isRefs := bool[6]
		isRefs[0] = true
		
		return GetFuncType(objs.Size(),typs[0]&,isRefs[0]&,GTypeVoid,false,false)
	}
	CreateFunc := virtual !(TypeFunc^ tp, Vector.{BoxExeConstObj^} consts) -> BoxFunc^
	{
		assert(GVectorConstructor4 != null)
		if tp.Params.Size() == 5
			return GVectorConstructor4.CreateFunc(tp,consts)
		if tp.Params.Size() == 4
			return GVectorConstructor3.CreateFunc(tp,consts)
		if tp.Params.Size() == 3
			return GVectorConstructor2.CreateFunc(tp,consts)
		if tp.Params.Size() == 2
		{
			switch GetVecXTypeSize(tp.Params[0].ItType)
			{
			case 2
				return GVectorConstructor2Scalar.CreateFunc(tp,consts)
			case 3
				return GVectorConstructor3Scalar.CreateFunc(tp,consts)
			case 4
				return GVectorConstructor4Scalar.CreateFunc(tp,consts)
			}
		}
		assert(false)
	}
}

BuiltInVecScalarMult := class extend BoxFuncCommon
{
	this := !(TypeFunc^ tp) -> void
	{
		SetType(tp)
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream
		typ := lst[0].GetType()
		siz := GetVecXTypeSize(typ)

		lst[^].PrintPre(ctx)

		f^ << "%Temp" << resId << " = fmul "
		typ.PrintType(ctx)
		f^ << " "
		lst[0].PrintUse(ctx)
		f^ << " , "
		lst[1].PrintUse(ctx)
		f^ << "\n"
		f^ << "%T" << resId << " = call fast float @llvm.vector.reduce.fadd.f32.v" 
		f^ << siz << "f32(float -0.0,<" << siz << " x float> %Temp" << resId << ")"
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
}

BuiltInVecModifByScalar:= class extend BoxFuncCommon
{
	operatorType := Str
	this := !(TypeFunc^ fType,Str operType) -> void
	{
		SetType(fType)
		operatorType = operType
	}
	CreateCall := virtual !(Vector.{BoxExeObj^} objs,FuncInputObjects^ iObjs) -> BoxExeObj^
	{
		base := objs[0].GetType()

		locVar := new LocalVar("anon",base,true,false)
		CurrentFunc?.AddLocalVar(locVar)

		cCall := locVar.CreateVarCall()
		constrObj := CreateFuncCall(". this",![cCall,objs[1]])
		assert(constrObj != null)
		locVar.extraWork.Push(constrObj?)

		scalarAsVec := new ConstructorCallWrapper(locVar,cCall)

		return CreateFuncCall(operatorType,![objs[0],scalarAsVec ])
	}
}

CreateBuiltInVec:= !() -> void
{
	bSpace := CodeSpaces[-1]&

	baseOfVec := GTypeFloat
	for x : ![GTypeVec4f,GTypeVec3f,GTypeVec2f,GTypeQuantf] // float base
	{
		baseRefed := GetFuncType(![x,x],![true,false],x)
		baseAdd := GetFuncType(![x,x],![false,false],x)

		bSpace.Functions["+"].Push(new BuiltInIntBase(baseAdd,"fadd") )
		bSpace.Functions["-"].Push(new BuiltInIntBase(baseAdd,"fsub") )
		bSpace.Functions["*"].Push(new BuiltInIntBase(baseAdd,"fmul") )
		bSpace.Functions["/"].Push(new BuiltInIntBase(baseAdd,"fdiv") )

		bSpace.Functions["+="].Push(new BuiltInIntEqAndOper(baseRefed,"fadd") )
		bSpace.Functions["-="].Push(new BuiltInIntEqAndOper(baseRefed,"fsub") )
		bSpace.Functions["*="].Push(new BuiltInIntEqAndOper(baseRefed,"fmul") )
		bSpace.Functions["/="].Push(new BuiltInIntEqAndOper(baseRefed,"fdiv") )

		bSpace.Functions["<+>"].Push(new BuiltInVecScalarMult(GetFuncType(![x,x],GTypeFloat)) )
	}
	for xIndex : ![23,24,25] // int base
	{
		x := TypeTable[xIndex]
		baseRefed := GetFuncType(![x,x],![true,false],x)
		baseAdd := GetFuncType(![x,x],![false,false],x)

		bSpace.Functions["+"].Push(new BuiltInIntBase(baseAdd,"add") )
		bSpace.Functions["-"].Push(new BuiltInIntBase(baseAdd,"sub") )
		bSpace.Functions["*"].Push(new BuiltInIntBase(baseAdd,"mul") )
		bSpace.Functions["div"].Push(new BuiltInIntBase(baseAdd,"sdiv") )

		bSpace.Functions["+="].Push(new BuiltInIntEqAndOper(baseRefed,"add") )
		bSpace.Functions["-="].Push(new BuiltInIntEqAndOper(baseRefed,"sub") )
		bSpace.Functions["*="].Push(new BuiltInIntEqAndOper(baseRefed,"mul") )
		bSpace.Functions["/="].Push(new BuiltInIntEqAndOper(baseRefed,"sdiv") )
	}
	ForEachVecType(x ==> {

		storeType := GetFuncType(![x->{Type^},x],![true,false],GTypeVoid)
		base := x.Base

		bSpace.Functions["="].Push(new BuiltInPrimeStore(storeType))

		infixScalar := GetFuncType(![x,base],![true,false],GTypeVoid)

		for it : !["+=","-=","*=","/="]
			bSpace.Functions[it].Push(new BuiltInVecModifByScalar(infixScalar,it))

		oper2 := GetFuncType(![x,base],![false,false],x)
		for it : !["+","-","*","/"]
			bSpace.Functions[it].Push(new BuiltInVecModifByScalar(oper2,it))

	})

	bSpace.Templates["."].Push(new BuiltInGetVecTemplate())

	bSpace.Templates[". this"].Push(new BuiltInVecConstrTemplate)
}

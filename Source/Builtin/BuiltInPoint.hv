
CreateBuiltinsPoint := !() -> void
{
	bSpace := CodeSpaces[-1]&

	GPointStoreTemplate = new PointStoreTemplate()
	bSpace.Templates["="].Push(GPointStoreTemplate)

	bSpace.Templates["[]"].Push(new PointArrayGetTemplate())


	x := GTypeVoidP
	cmpType := GetFuncType(![x,x],GTypeBool)

	GPointCmpVoidFuncEQ = new BuiltInIntBase(cmpType,"icmp eq") 
	GPointCmpVoidFuncNE = new BuiltInIntBase(cmpType,"icmp ne") 
	bSpace.Functions["=="].Push(GPointCmpVoidFuncEQ)
	bSpace.Functions["!="].Push(GPointCmpVoidFuncNE)


	bSpace.Functions["<=>"].Push(new BuiltInPointSpaceship() )
}

BuiltInPointSpaceship := class extend BoxFuncCommon
{
	this := !() -> void
	{
		SetType(GetFuncType(![GTypeVoidP,GTypeVoidP],GTypeInt))
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream

		lst[^].PrintPre(ctx)

		f^ << "%TA" << resId << " = ptrtoint i8* "
		lst[0].PrintUse(ctx)
		f^ << " to i" << GArchBitSize << "\n"

		f^ << "%TB" << resId << " = ptrtoint i8* "
		lst[1].PrintUse(ctx)
		f^ << " to i64\n"

		f^ << "%TIsEq" << resId << " = icmp eq i64 %TA" << resId << ", %TB" << resId << "\n"
		f^ << "%TIsBg" << resId << " = icmp ugt i64 %TA" << resId << ", %TB" << resId << "\n"
		f^ << "%TBgN" << resId << " = select i1 %TIsBg" << resId << " , i32 1, i32 -1\n"


		f^ << "%T" << resId << " = select i1 %TIsEq" << resId << " , i32 0 , i32 %TBgN" << resId
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
		//note: output MUST be s32
	}
}

PointStoreTemplate := class extend BoxFuncTemplate
{
	this := !() -> void
	{
	}
	ToFuncType := virtual !(BoxExeObj^[] objs, BoxExeConstObj^[] consts) -> TypeFunc^
	{
		if objs.Size() != 2
			return null
		if not objs[0].IsMem()
			return null
		a := objs[0].GetType()
		if a is in TypePoint or a is TypeArrayView
		{
			return GetFuncType(2,![a,a],![true,false],GTypeVoid,false,false)
		}
		return null
	}
	CreateFunc := virtual !(TypeFunc^ tp, BoxExeConstObj^[] consts) -> BoxFunc^
	{
		return new BuiltInPrimeStore(tp)
	}
}
GPointStoreTemplate := PointStoreTemplate^
GPointCmpVoidFuncEQ := BoxFunc^
GPointCmpVoidFuncNE := BoxFunc^

PointArrayGetTemplate := class extend BoxFuncTemplate
{
	this := !() -> void
	{
	}
	ToFuncType := virtual !(BoxExeObj^[] objs, BoxExeConstObj^[] consts) -> TypeFunc^
	{
		if objs.Size() != 2
			return null
		a := objs[0].GetType()
		if a is in TypePoint or a is TypeArr
		{
			a2 := a.Base.GetPoint()
			if a is in TypePoint and a.Base is TypeClass
			{
				if a.Base->{TypeClass^}.ToClass.IsViewType()
				{
					return null
				}
			}
			return GetFuncType(2,![a2,GTypeInt],null,a.Base,true,false)
		}
		return null
	}
	CreateFunc := virtual !(TypeFunc^ tp, BoxExeConstObj^[] consts) -> BoxFunc^
	{
		return new BuiltInPointGetItem(tp)
	}
}

BuiltInPointGetItem := class extend BoxFuncCommon
{
	this := !(TypeFunc^ iType) -> void
	{
		SetType(iType)
	}
	PrintFuncCall := virtual !(LLVMWriteContext^ ctx,int resId,int debId,ExeDownList lst) -> void
	{
		f := ctx.writeStream

		lst[^].PrintPre(ctx)
		
		tFunc := GetType()->{TypeFunc^}
		tp := tFunc.Params[0].ItType
		bs := Type^
		if tp == GTypeVoidP
		{
			bs = GTypeChar
		}else{
			bs = tp.Base
		}

		f^ << "%T" << resId << " = getelementptr " 
		bs.PrintType(ctx)
		f^ << " , "
		tp.PrintType(ctx)
		f^ << " "
		lst[0].PrintUse(ctx)
		f^ << " ,  "
		lst[1].GetType().PrintType(ctx)
		f^ << " "
		lst[1].PrintUse(ctx)
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
}


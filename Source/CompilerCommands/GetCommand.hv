TryGetCommand := !(Token^ itm) -> BoxExeObj^ 
{
	if itm == null or itm.Down == null
		return null
	cmd := itm.Down->{TokenIndent^}.Value

	for CmpFnc : #MarkedFunctions("CompilerCommand")
	{
		return CmpFnc(cmd,itm)?
	}

	if cmd == "#DebugMode"
	{
		if DebugMode
			return GBoolTrue
		return GBoolFalse
	}

	if cmd == "#File" or cmd == "#FileLocal"
	{
		return TryEmbedFile(itm)
	}
	if cmd == "#Regexp"
	{
		return CommandGetRegexp(itm)	
	}
	if cmd == "#OS"
	{
		if targetLinux
			return GetExeString(StringSpan("Linux").GetString())
		if targetWindows
			return GetExeString(StringSpan("Windows").GetString())
		assert(false)
	}
	if cmd == "#MarkedFunctions" or cmd == "#MarkFunction"
	{
		return TryGetMarkedFunctions(itm)
	}
	if cmd == "#ForceVirtualFunctions"
	{
		return TryForceVirtualFunctions(itm)
	}
	if cmd == "#TLSSize"
	{
		return GetExeInt(TLSSize)
	}
	if cmd == "#Var"
		return GetCompilerCommand(itm) 
	if cmd == "#ThisClass"
	{
		itrScope := CurrentScope
		while itrScope != null
		{
			if itrScope is in BoxClass
				break
			itrScope = itrScope.GetParentScope()
		}
		if itrScope == null
		{
			CurrentWork?.SetErrorMessage(itm.MakeError("Command is not in class"))
			return null
		}
		return GetConstType(itrScope->{BoxClass^}.ItType)
	}

	return TryAddCallCommand(itm)?
	return TryAddCreateModule(itm)?

	if not CurrentFuncWork.isFailed
	{
		CurrentWork?.SetErrorMessage(itm.MakeError("Command not found"))
		return null
	}else{
		return null
	}
}

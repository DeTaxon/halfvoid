GetBoxFor := !(Object^ dat) -> BoxFor^
{

	itemName := string
	indName := string

	itemName = null

	iterY := dat.Down.Right

	Names := @temp new List.{char^}
	indNames := @temp new List.{char^}
	Downs := @temp new List.{Object^}
	if iterY.Right.Right == null
	{
		Names.Push("it")
		Downs.Push(iterY)
		indNames.Push("it_ind")
		iterY = iterY.Right
	}else{
		while iterY.Right != null
		{
			if iterY.Right.GetValue() == ","
			{
				Names.Push(iterY->{ObjIndent^}.MyStr)
				iterY = iterY.Right.Right
				indNames.Push(iterY->{ObjIndent^}.MyStr)
				iterY = iterY.Right.Right
			}else{ // ":"
				Names.Push(iterY->{ObjIndent^}.MyStr)
				iterY = iterY.Right.Right
				indNames.Push(null->{string})
			}
			Downs.Push(iterY)
			iterY = iterY.Right
			if iterY.GetValue() == ","
				iterY = iterY.Right

		}
	}
	preRes :=  new BoxForOldFashionMulti(Names^,indNames^,Downs^,iterY)
	preRes.Line = dat.Down.Line

	if dat.Line?.itAttrs.Size() != 0
	{
		preRes.attrs = dat.Line.itAttrs&
	}
	return preRes
}

BoxFor := class extend Object
{
}


BoxForOldFashionMulti := class extend BoxFor
{
	ItId := int
	IsStep1 := bool

	itemsCount := int
	IsEndFunc := Object^

	InputItems := RawArray.{BoxForItem^}

	attrs := AttrArrayType^

	destroyFuncCount := int

	EnabledIIndex := bool
	CreatedIIndexNames := List.{char^}

	holder := Object^

	endLabel := BoxLabel^
	this := !(List.{char^} names, List.{char^} f_ind,List.{Object^} items,Object^ itBlock) -> void
	{
		endLabel = new BoxLabelAnon()
		for items
		{
			if it.IsConst 
				continue
			mayb := TryCompute(it)
			if mayb != null
			{
				oldLine := it.Line
				it = mayb.Clone()
				it.Line = oldLine
			}
		}


		Down = itBlock
		Down.Left = null
		itemsCount = items.Size()
		iter := Down

		for i : itemsCount, itm : items
		{
			iter.Right = itm
			iter.Right.Left = iter
			iter = iter.Right
		}
		iter.Right = null
		Down.SetUp(this&)

		InputItems.Resize(itemsCount)
		InputItems[^] = new BoxForItem

		for it,i : names
			InputItems[i].Name = it
		for it,i : f_ind
			InputItems[i].IndName = it

		WorkBag.Push(this&,State_PreGetUse)

		IsStep1 = true

		ItId = GetNewId()
	}
	useDecorator := bool
	decLambdaCall := Object^
	decLambdaCallFunc := BoxFunc^
	DoTheWork := virtual !(int pri) -> void
	{
		if visitedWork[pri] return void
		else if pri != State_GetUse  visitedWork << pri

		if pri == State_Start
		{
			Down.Line = Line
			WorkBag.Push(this&,State_PreGetUse)
		}
		if pri == State_PreGetUse
		{
			while TryParseMacro(Down.Right,this&) != null {}


			WorkBag.Push(this&,State_GetUse)
			if Left? is ObjTemplateType //DECORATOR
			{
				TType := Left
				cmdName := Left->{ObjTemplateType^}.MyStr
				itBox := new FuncInputBox
				
				bodyL := GetFuncType(![GTypeVoidP],GTypeVoid).GetLambda()
				innerL := GetFuncType(![GTypeVoidP,bodyL],GTypeVoid).GetLambda()
				outerL := GetFuncType(![GTypeVoidP,innerL],GTypeVoid).GetLambda()

				itBox.itPars.Emplace(outerL,false)

				fnc := FindFunc("@"sbt + cmdName + " for",this&,itBox^,false)

				if fnc == null
					EmitError("Decorator not found")

				tmpObj := Object
				lmbd := UNext(this&, new SBoostLambda(),1)->{SBoostLambda^}
				ReplaceNode(lmbd,tmpObj&)
				lmbd.Down = this&
				this.Up = lmbd
				lmbd.ApplyFunc(outerL,false)
				dCall := MakeSimpleCall(fnc,lmbd)
				ReplaceNode(tmpObj&,dCall)


				gotL := lmbd.GetItem("_1")
				assert(gotL != null)
				lCall := new ParamNaturalCall("_1",gotL)

				itBx := @temp new FuncInputBox
				itBx.itPars.Emplace(innerL,false)
				itBx.itPars.Emplace(bodyL,false)

				lCallFunc := GlobalLambdaCall.GetFunc(itBx^)
				assert(lCallFunc != null)

				useDecorator = true
				decLambdaCall = lCall
				decLambdaCallFunc = lCallFunc

			}

			WorkBag.Push(Down.Right[^],State_Start)
		}
		if pri == State_GetUse
		{
			if(Down.Right is FieldHolder)
			{
				asPrm := this&
				asPrm->SetType(MetaBoxForFields)
				WorkBag.Push(this&,State_PrePrint)
				return void
			}
			if(Down.Right is MetaFuncParamsHolder)
			{
				asPrm := this&
				asPrm->SetType(MetaBoxForFuncParams)
				WorkBag.Push(this&,State_PrePrint)
				return void
			}
			if(Down.Right is MetaClassFuncsHolder)
			{
				asPrm := this&
				asPrm->SetType(MetaBoxForClassFuncs)
				WorkBag.Push(this&,State_PrePrint)
				return void
			}
			if(Down.Right is CmdMarkedHolder)
			{
				asPrm := this&
				asPrm->SetType(MetaBoxForMarked)
				WorkBag.Push(this&,State_GetUse)
				return void
			}
		
			if IsStep1
			{
				InputItems.Resize(itemsCount)

				Downs:= Queue.{Object^}() ; $temp
				for i : itemsCount
				{
					if Down.Right.GetType() == null
					{
						EmitError("can not evaluate type in for each\n")
						continue
					}

					someBox := @temp new FuncInputBox
					someBox.itPars.Emplace(Down.Right.GetType(),Down.Right.IsRef())
					if Down.Right.Line != null for q,k : Down.Right.Line.itAttrs
					{
						someBox.itAttrs[k] = q
					}
					if Down.Right is MetaItemWrapper
					{
						someBox.itMetaPtr = Down.Right->{MetaItemWrapper^}.ptrToBlock
					}


					func := FindFunc("~For",this&,someBox^,false)
					if func == null 
					{
						Down.Right.GetType()
						EmitError("can not load ~For func\n")
						continue
					}
					if func.MyFuncType.RetType == null
					{
						EmitError("can not deduce return type of function ~For\n")
						continue
					}

					rType := func.MyFuncType.RetType

					if rType is TypeFuncLambda
					{
						asFunc := rType.Base->{TypeFunc^}
						if asFunc.RetType == GTypeVoid
							EmitError("Return type of lamda function can not be void")
					}else{
						if (not rType is TypeClass)
							and (not (rType is TypePoint and rType.Base is TypeClass))
						{
							EmitError("~For have to return class or pointer to class\n")
							continue
						}
					}

					InputItems[i].ProxyFunc = func

					tmp := Down.Right
					PopOutNode(tmp)
					preSet := MakeSimpleCall(func,tmp)
					preSet.Line = tmp.Line
					if tmp.Line == null
					{
						tmp.Up.Print(0)
					}
					preSet.Up = this&
					Downs.Push(preSet)
				}

				iter := Down
				iter2 := Downs.Start
				iter = Down
				WorkBag.Push(this&,State_GetUse)
				while iter2 != null
				{
					iter.Right = iter2.Data
					iter.Right.Left = iter
					iter = iter.Right
					iter2.Data.Up = this&
					iter2 = iter2.Next
					WorkBag.Push(iter,State_GetUse)
				}
				Down.SetUp(this&)
				IsStep1 = false
			}else{
				visitedWork << pri

				if useDecorator
				{
					bodyL := GetFuncType(![GTypeVoidP],GTypeVoid).GetLambda()

					MakeItBlock(Down)
					bodyLambda := UNext(Down, new SBoostLambda(),1)->{SBoostLambda^}
					bodyLambda.ApplyFunc(bodyL,false)

					tmpObj := new Object ; $temp
					ReplaceNode(Down,tmpObj)
					decLambdaCall.Right = bodyLambda
					bodyLambda.Left = decLambdaCall
					bodyCall := MakeSimpleCall(decLambdaCallFunc,decLambdaCall)
					ReplaceNode(Down,bodyCall)
					//PopOutNode(TType)
					MakeItBlock(Down)
				}
				MakeItBlock(Down)
				WorkBag.Push(Down,State_Start)
				iter := Down.Right
				for i : itemsCount
				{
					asNeed2 := iter->{SomeFuncCall^}
					itType := InputItems[i].ProxyFunc.MyFuncType.RetType

					//if itType is TypeFuncLambda
					//{
					//	callFunc := @temp new FuncInputBox
					//	callFunc.itPars.Emplace(itType,false)
					//	LambdaCallFunc[i] = FindFunc("()",this&,callFunc^,false)
					//	LambdaIsFinishedFunc[i] = FindFunc("IsFinished",this&,callFunc^,false)

					//	assert(LambdaCallFunc[i] != null)
					//	assert(LambdaIsFinishedFunc[i] != null)

					//	itmId := GetAlloc(this&,itType)
					//	itm := new LocalParam(itType,itmId,false)

					//	ProxyResults[i] = itm

					//	test2 := new ParamNaturalCall("",itm)
					//	test2.Line = iter.Line
					//	UnrefFuncs[i] = MakeSimpleCall(LambdaCallFunc[i]->{BoxFunc^},test2)
					//	UnrefFuncs[i].Line = this.Line
					//	UnrefFuncs[i].Up = this&

					//	Params[i] = new RetFuncParamV2(UnrefFuncs[i],this&)
					//	Params[i].Up = this&

					//	test2 = new ParamNaturalCall("",itm)
					//	test2.Line = iter.Line
					//	IsEndFunc = MakeSimpleCall(LambdaIsFinishedFunc[i]->{BoxFunc^},test2)
					//	IsEndFunc.Line = this.Line
					//	IsEndFunc.Up = this&

					//	continue
					//}

					InputItems[i]->SetType(BoxForItemOnClass)

					icl := InputItems[i]->{BoxForItemOnClass^}

					ForItem := LocalParam^

					if itType is TypeClass
					{
						ItId2 := asNeed2.GetItAllocId()
						ForItem = new LocalParam(itType,ItId2)
					}else{
						assert(itType is TypePoint)
						newId := GetAlloc(this&,itType)
						ForItem = new LocalParam(itType.Base,newId,true)
						InputItems[i].ProxyResult = ForItem
					}
					ForItem.Line = iter.Line
					
					asNeed := BoxClass^
					if itType is TypePoint
					{
						asNeed = itType.Base->{TypeClass^}.ToClass						
					} else {
						asNeed = itType->{TypeClass^}.ToClass
					}

					emptyBox := @temp new FuncInputBox
					emptyBox.itPars.Emplace(asNeed.ClassType,true)
					if iter.Line != null for q,k : iter.Line.itAttrs
					{
						emptyBox.itAttrs[k] = q
					}

					IncFuncP := asNeed.GetFunc("Inc",emptyBox^,false)
					UnrefFuncP := asNeed.GetFunc("^",emptyBox^,false)
					IsInvP := asNeed.GetFunc("IsInvalid",emptyBox^,false)
					InputItems[i].DestroyFunc = asNeed.GetFunc("Destroy",emptyBox^,false)
					if InputItems[i].DestroyFunc != null
					{
						destroyFuncCount += 1
					}
					

					if IncFuncP == null {
						EmitError("incorrect for iterator, need increment\n")
						return void
					}
					if UnrefFuncP == null {
						EmitError("incorrect for iterator, need ^\n")
						return void
					}


					test := new ParamNaturalCall("",ForItem->{Object^})
					test.Line = iter.Line
					InputItems[i].UnrefFunc = MakeSimpleCall(UnrefFuncP,test)
					InputItems[i].UnrefFunc.Line = this.Line
					InputItems[i].UnrefFunc.Up = this&
					test = new ParamNaturalCall("",ForItem->{Object^})
					test.Line = iter.Line
					InputItems[i].IncFunc = MakeSimpleCall(IncFuncP,test)
					InputItems[i].IncFunc.Line = this.Line
					InputItems[i].IncFunc.Up = this&

					if IsInvP != null
					{
						test = new ParamNaturalCall("",ForItem->{Object^})
						test.Line = iter.Line
						InputItems[i].IsInvalid = MakeSimpleCall(IsInvP,test)
						InputItems[i].IsInvalid.Line = this.Line
						InputItems[i].IsInvalid.Up = this&
					}

					if i == 0
					{
						IsEndFuncP := asNeed.GetFunc("IsEnd",emptyBox^,false)
						test = new ParamNaturalCall("",ForItem->{Object^})
						test.Line = iter.Line
						IsEndFunc = MakeSimpleCall(IsEndFuncP,test)
						IsEndFunc.Line = this.Line
						IsEndFunc.Up = this&
					}

					InputItems[i].Param = new RetFuncParamV2(InputItems[i].UnrefFunc,this&)
					InputItems[i].Param.Up = this&

					if InputItems[i].IndName != null
					{
						itFunc4 := asNeed.GetFunc("Ind",emptyBox^,false)
						if itFunc4 == null
						{
							//EmitError("Can not get index item\n")
							EnabledIIndex = true
							InputItems[i].IndParam = new FuncParam("ForIndex"sbt + ItId,GTypeInt,false)
							InputItems[i].IndParam.Up = this&

							if DebugMode
							{
								CreatedIIndexNames.Push(InputItems[i].IndName)
							}
						}else{
							test = new ParamNaturalCall("",ForItem->{Object^})
							InputItems[i].IndFunc = MakeSimpleCall(itFunc4,test)
							InputItems[i].IndParam = new RetFuncParamV2(InputItems[i].IndFunc,this&)
							InputItems[i].IndParam.Up = this&
							InputItems[i].IndFunc.Line = this.Line
							InputItems[i].IndFunc.Up = this&
						}
					}
					iter = iter.Right
				}
				if destroyFuncCount != 0
				{
					fnc := GetBoxFuncContainer(this&)
					if fnc != null
					{
						fnc.DoDefer()
					}
				}
			}
		}
	}
	GetItem := virtual !(string name) -> Object^
	{
		for it : InputItems
		{
			if it.Name == name
				return it.Param
			if it.IndName == name
				return it.IndParam
		}
		return null
	}
	
	GetDeferUsageVerticalSize := virtual !() -> int
	{
		return Down.GetDeferUsageVerticalSize()	+ destroyFuncCount
	}
	PrintDeferUse := virtual !(TIOStream f, BoxFuncContainer^ bd,BoxBlock^ blk, int depth,int^ labelIter) -> void
	{
		Down.PrintDeferUse(f,bd,blk,depth,labelIter)

		for itr,i : Down.Right
		{
			if InputItems[i].DestroyFunc == null
				continue

			j := destroyFuncCount - i - 1
			f << "br label %DeferLabel" << labelIter^ << "\n"
			f << "DeferLabel" << labelIter^ << ":\n"

			if InputItems[i].DestroyFunc is BoxFuncBody //TODO in BoxFunc
			{
				if IsGCClass(itr.GetType())
				{
					itr.PrintPointPre(f)

					f << "%DestrObj" << ItId << "n" << j << " = bitcast "
					itr.PrintPointUse(f)
					f << " to i8*\n"

					f << "call void @" << gcCallDestroy.OutputName << "("
					f << "i8* %DestrObj" << ItId << "n" << j
					f << ")\n"
					continue
				}
				if InputItems[i].DestroyFunc->{BoxFunc^}.IsVirtual
				{
					//TODO add virtual destructor support
					continue
				}
			}


			//itr.PrintPointPre(f) //TODO without that works on pure luck
			
			fnc := InputItems[i].DestroyFunc->{BoxFunc^}
			f << "call void @" << fnc.OutputName << "("
			itr.PrintPointUse(f)
			f << ")\n"
			//f << "%Ptr" << ItId << "num" << i << " = bitcast "
			//itr.PrintPointUse(f) f << " to i8*\n"
			//f << "%TFunc" << ItId << "num" << i << " = bitcast " << fnc.MyFuncType.GetName() << "* @" << fnc.OutputName << " to void(i8*)*\n"
			//f << "call void @" << deferAddDefer.OutputName << "(void(i8*)* %TFunc" << ItId << "num" << i << " , i8* %Ptr"<<ItId<<"num"<<i<<" )"
			//f << "\n"

			labelIter^ -= 1
		}
	}

	GetDeferUsage := virtual !() -> int
	{
		val := GetDeferUsageDown()
		if destroyFuncCount != 0
		{
			val = max(val,1)
		}
		return val
	}
	deferId := int
	deferLabel := int
	PrintDeferInBlock := virtual !(TIOStream f, int itId,int^ labelSetIter) -> void
	{
		//Down.Right.PrintDeferInBlock(f,itId,labelSetIter)
		deferId = itId
		deferLabel = labelSetIter^
		labelSetIter^ += Down.GetDeferUsageVerticalSize() + destroyFuncCount
	}
	PrintInBlock := virtual !(TIOStream f) -> void
	{
		debId := -1
		if DebugMode debId = CreateDebugCall(this&)
		
		for itr,i : Down.Right
		{
			if InputItems[i].ProxyResult == null
			{
				itr.PrintPre(f)
			}else{
				itr.PrintPre(f)
				f << "store "
				itr.PrintUse(f)
				f << " , "
				itr.GetType().PrintType(f)
				f << "* " << InputItems[0].ProxyResult.GetMainPtr(0) //TODO why ZERO?
				f << "\n"
			}
		
			if InputItems[i].DestroyFunc != null
			{
				f << "store i8 " << deferLabel << " , i8* %DeferStack" << deferId << "\n"
				deferLabel += 1
			}
		}


		Checks := 0
		if InputItems[^].IsInvalid != null //TODO Contain
			Checks += 1

		if Checks > 0
		{
			for it : InputItems
			{
				if it.IsInvalid != null
				{
					
					it.IsInvalid.PrintPre(f)
					f << "br i1 " << it.IsInvalid.GetName() << ", label %" << endLabel.GetLabel()
					if Checks > 1
					{
						f << ", label %NextCheck" << Checks << "\n"
						f << "NextCheck" << Checks << ":\n"
					}else{
						f << ", label %start" << ItId << "\n" // no need in standart for
						f << "start" << ItId << ":\n"
					}
				}
				Checks -= 1
			}
		}else{
			f << "br label %start" << ItId << "\n" // no need in standart for
			f << "start" << ItId << ":\n"
		}

		createdIInts := List.{int}() ; $temp

		if debId != -1 for it : CreatedIIndexNames
		{
			newRes := CreateDbgLocVar(Down,GTypeInt,it)
			if newRes != -1
				createdIInts.Push(newRes)
		}

		if EnabledIIndex {
			f << "br label %ExtraStart" << ItId << "\n"
			f << "ExtraStart" << ItId << ":\n"
			f << "%ForIndex" << ItId << " = phi i32 [0,%start" << ItId << "] , [%FIndex" << ItId << ", %ExtraEnd" << ItId << "]\n"

			for it : createdIInts
			{
				f << "call void @llvm.dbg.value(metadata i32 %ForIndex" << ItId << " ,metadata !" << it << ",metadata !DIExpression()), !dbg !"<<debId<<"\n" 
			}
		}

		IsEndFunc.PrintPre(f)
		f << "br i1 " << IsEndFunc.GetName() << " , label %" << endLabel.GetLabel() << " , label %Next" << ItId << "\n"
		f << "Next" << ItId << ":\n"


		for it,i : InputItems
		{
			if it.UnrefFunc.IsRef() it.UnrefFunc.PrintPointPre(f) else it.UnrefFunc.PrintPre(f)
			if it.IndFunc != null
			{
				if it.IndFunc.IsRef()
				{
					it.IndFunc.PrintPointPre(f)
				}else{
					it.IndFunc.PrintPre(f)
				}
			}
			if it.Param? is RetFuncParamV2 and it.Name != null and debId != -1
			{
				it.Param->{RetFuncParamV2^}.PrintForDebugDeclare(f,it.Name,Down,debId)
			}
			if it.Param != null
				it.Param.DoStore(f)
			if it.IndFunc != null and it.IndParam? is RetFuncParamV2 and it.IndName != null and debId != -1
			{
				it.IndParam->{RetFuncParamV2^}.PrintForDebugDeclare(f,it.IndName,Down,debId)
			}
			if it.IndParam != null
				it.IndParam.DoStore(f)
		}
		Down.PrintDeferInBlock(f,deferId,deferLabel&)
		Down.PrintInBlock(f)
		
		f << "br label %IncFuncs" << ItId << "\n"
		f << "IncFuncs" << ItId << ":\n"
		InputItems[^].IncFunc.PrintPre(f)

		if EnabledIIndex
		{
			f << "br label %ExtraEnd" << ItId << "\n"
			f << "ExtraEnd" << ItId << ":\n"
			f << "%FIndex" << ItId << " = add i32 %ForIndex" << ItId << " , 1\n"
			f << "br label %ExtraStart" << ItId << "\n"
		}else{
			f << "br label %start" << ItId << "\n"
		}

		if labelContinue != null
		{
			labelContinue.PrintLabel(f)
			Down->{BoxBlock^}.PrintDeferInBlockUse(f)
			f << "br label %IncFuncs" << ItId << "\n"
		}
		if labelBreak != null
		{
			labelBreak.PrintLabel(f)
			Down->{BoxBlock^}.PrintDeferInBlockUse(f)
			f << "br label %" << endLabel.GetLabel() << "\n"
		}
		endLabel.PrintLabel(f)
	}

	labelContinue := BoxLabel^
	labelBreak := BoxLabel^

	GetOutPath := virtual !(Object^ itm, int typ, int size) -> BoxLabel^
	{
		if typ == PATH_RETURN
		{
			return Up.GetOutPath(this&,typ,size) //poke
		}
		if typ == PATH_CONTINUE
		{
			if size == 0{
				if labelContinue == null
					labelContinue = new BoxLabelAnon()
				return labelContinue
			}
			return Up.GetOutPath(itm,typ,size - 1)
		}
		if typ == PATH_BREAK
		{
			if size == 0{
				if labelBreak == null
					labelBreak = new BoxLabelAnon()
				return labelBreak
			}
			return Up.GetOutPath(itm,typ,size - 1)
		}
		return null
	}
	GetBadLabel := !(Object^ prebNode) -> Object^
	{
		return endLabel
	}
	GetValue := virtual !() -> string
	{
		return "~~for()"
	}
}



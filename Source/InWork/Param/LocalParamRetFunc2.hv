RetFuncParamV2 := class extend MemParam
{
	ToCall := Object^
	AllcId := int
	this := !(Object^ func,Object^ nUp) -> void
	{
		Up = nUp
		ToCall = func
		ResultType = func.GetType()
	}
	IsRef := virtual !() -> bool
	{
		return true
	}

	NotifyPointUse := virtual !() -> void
	{
		if ToCall.IsRef()
		{
			AllcId = GetAlloc(this&,ResultType.GetPoint())
			this&->SetType(RetFuncParamV2RefedRef)
		}else{
			AllcId = GetAlloc(this&,ResultType)
			this&->SetType(RetFuncParamV2Refed)
		}
	}

	PrintPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPre(f)
		if ToCall.IsRef()
		{
			f << "%T" << newInd << " = load " << ResultType.GetName() << " , " 
			ToCall.PrintPointUse(f)
			if DebugMode and debId != -1
				f << ", !dbg !" << debId 
			f << "\n"
		}
	}
	PrintUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		if ToCall.IsRef()
		{
			f << ResultType.GetName() << " %T" << newInd << "\n"
		}else{
			ToCall.PrintUse(f)
		}
	}
	PrintPointPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPointPre(f)
	}
	PrintPointUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		ToCall.PrintPointUse(f)
	}
	GetName := virtual !(int newInd) -> string
	{
		if ToCall.IsRef()
		{
			return "%T"sbt + newInd
		}
		return ToCall.GetName()
	}
	GetType := virtual !() -> Type^ { 
		return ResultType 
	}
	GetPointName := virtual !(int newInd) -> string
	{
		return ToCall.GetPointName()
	}
	PrintForDebugDeclare := virtual !(TIOStream f,char^ forceName,Object^ ptrPos,int debId) -> void
	{
		if debId == -1 return void

		outId := CreateDbgLocVar(ptrPos,ResultType,forceName,ToCall.IsRef())
		if debId != -1 and outId != -1
		{
			f << "call void @llvm.dbg.value"
			f << "(metadata "
			if ToCall.IsRef() PrintPointUse(f,0,-1) else PrintUse(f,0,-1)
			f << " , metadata !" << outId << " , metadata !DIExpression()) , !dbg !" << debId << "\n"
		}
	}
}
RetFuncParamV2Refed := class extend RetFuncParamV2
{
	NotifyPointUse := virtual !() -> void {}

	DoStore := virtual !(TIOStream f, int debId) -> void
	{
		rn := ResultType.GetName()
		//ToCall.PrintPre(f)
		f << "store "
		ToCall.PrintUse(f)
		f << " , " << rn << "* %T" << AllcId << "\n" //TODO DEBUGID
	}
	PrintPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPre(f)
		f << "%T" << newInd << " = load " << ResultType.GetName() << " , "
		f << ResultType.GetName() << "* %T" << AllcId 
		if DebugMode and debId != -1
			f << ", !dbg !" << debId 
		f << "\n"
	}
	PrintUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		f << ResultType.GetName() << " %T" << newInd << "\n"
	}
	PrintPointPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPointPre(f)
	}
	PrintPointUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		f << ResultType.GetName() << "* %T" << AllcId << "\n"
	}
	GetName := virtual !(int newInd) -> string
	{
		return "%T"sbt + newInd
	}
	GetType := virtual !() -> Type^ { 
		return ResultType 
	}
	GetPointName := virtual !(int newInd) -> string
	{
		return "%T"sbt + AllcId
	}
	PrintForDebugDeclare := virtual !(TIOStream f,char^ forceName,Object^ ptrPos,int debId) -> void
	{
		if debId == -1 return void

		outId := CreateDbgLocVar(ptrPos,ResultType,forceName,IsRef())
		if debId != -1 and outId != -1
		{
			f << "call void @llvm.dbg.addr"
			f << "(metadata "
			PrintPointUse(f,0,-1)
			f << " , metadata !" << outId << " , metadata !DIExpression()) , !dbg !" << debId << "\n"
		}
	}
}
RetFuncParamV2RefedRef := class extend RetFuncParamV2
{
	NotifyPointUse := virtual !() -> void {}

	DoStore := virtual !(TIOStream f, int debId) -> void
	{
		rn := ResultType.GetName()
		//ToCall.PrintPre(f)
		f << "store "
		ToCall.PrintPointUse(f)
		f << " , " << rn << "** %T" << AllcId << "\n" //TODO DEBUGID
	}
	PrintPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPre(f)
		f << "%TPre" << newInd << " = load " << ResultType.GetName() << "* , " << ResultType.GetName() << "** %T" << AllcId 
		f << "%T" << newInd << " = load " << ResultType.GetName() << " , " << ResultType.GetName() << "* %TPre" << newInd 
		if DebugMode and debId != -1
			f << ", !dbg !" << debId 
		f << "\n"
	}
	PrintUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		f << ResultType.GetName() << " %T" << newInd << "\n"
	}
	PrintPointPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		//ToCall.PrintPointPre(f)
		f << "%T" << newInd << " = load " << ResultType.GetName() << "* , " << ResultType.GetName() << "** %T" << AllcId
		if DebugMode and debId != -1
			f << ", !dbg !" << debId 
		f << "\n"
	}
	PrintPointUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
		f << ResultType.GetName() << "* %T" << newInd << "\n"
	}
	GetName := virtual !(int newInd) -> string
	{
		return "%T"sbt + newInd
	}
	GetType := virtual !() -> Type^ { 
		return ResultType 
	}
	GetPointName := virtual !(int newInd) -> string
	{
		return "%T"sbt + newInd
	}
	PrintForDebugDeclare := virtual !(TIOStream f,char^ forceName,Object^ ptrPos,int debId) -> void
	{
		if debId == -1 return void

		outId := CreateDbgLocVar(ptrPos,ResultType,forceName,IsRef())
		if debId != -1 and outId != -1
		{
			f << "call void @llvm.dbg.addr"
			f << "(metadata "
			PrintPointUse(f,0,-1)
			f << " , metadata !" << outId << " , metadata !DIExpression()) , !dbg !" << debId << "\n"
		}
	}
}
GlobalParam := class extend MemParamCommon
{
	MainId := int
	IsThreadLocal := bool
	taskFieldId := int


	PrepareMainPtr := virtual !(TIOStream f,int newId) -> void {}
	GetMainPtr := virtual !(int newId) -> char^ {
		return "@T"sbt + MainId
	}

	this := !(Type^ th,Object^ toSet) -> void
	{
		ResultType = th
		Down = toSet
		Down?.Up = this&

		if th == GTypeVoid
		{
			this&->SetType(GlobalParamVoid)
		}else
		{
			MainId = GetNewId()
			WorkBag.Push(this&,State_PrePrint)
		}
	}
	DoTheWork := virtual !(int pri) -> void
	{
		if pri == State_PrePrint
		{
			if Up? is ObjParam
			{
				CreateDbgGlobalVar(Up,ResultType,Up->{ObjParam^}.MyStr,false) //TODO: global ref?
			}
		}
	}

	PrintArrData := !(TIOStream f, Type^ toPr,bool isFirst) -> void
	{
		asArr := toPr->{TypeArr^}

		if not isFirst
			f << asArr.GetName() << " "

		bs := asArr.Base
		f << "["
		for i : asArr.Size
		{
			if i != 0
				f << " , "
			if bs is TypeClass
			{
				PrintClassData(f , bs->{TypeClass^}.ToClass,false)
			}else{
				assert(bs is TypeArr)
				PrintArrData(f, bs,false)
			}
		}
		f << "]\n"
	}
	
	PrintParameters := !(TIOStream f,BoxClass^ parClass) -> bool
	{
		needComma := false

		f << "%ClassParams" << parClass.ClassId << "{"
		if parClass.Parent != null and parClass.Parent.ParamsCount() != 0
		{
			needComma = PrintParameters(f,parClass.Parent)
		}
		for itPr : parClass.Params
		{
			if needComma 
				f << " , "
			else needComma = true
			itCntV := TypeContainVTable(itPr.ResultType)
			if itPr.ResultType is TypeClass and itCntV
			{
				//f << itPr.ResultType.GetName() << " "
				PrintClassData(f,itPr.ResultType->{TypeClass^}.ToClass,false)
			}else{
				if itCntV
				{
					assert(itPr.ResultType is TypeArr)
					PrintArrData(f,itPr.ResultType,false)
				}else{
					f << itPr.ResultType.GetName() << " zeroinitializer"
				}
			}
		}
		f << "}"
		return true
	}
	PrintClassData := !(TIOStream f, BoxClass^ toPr, bool isFirst) -> void
	{
		if not isFirst
			f << toPr.ClassType.GetName() << " "
		addedVal := false
		f << "{"
		if toPr.Params.Size() == 0 and not toPr.ContainVirtual {
			f << "i8 0"
		}
	
		needComma := false
		if toPr.ContainVirtual
		{
			addedVal = true
			f << "%ClassTableType" << toPr.ClassId << "* @ClassTableItem" << toPr.ClassId 
			needComma = true
		}
		if toPr.ParamsCount() != 0 or not toPr.ContainVirtual
		{
			if needComma f << ","
			PrintParameters(f,toPr)
		}
		
		f << "}\n"
	}
	PrintGlobal := virtual !(TIOStream f) -> void
	{
		if ResultType == GTypeVoid
			return void
		f << "@T" << MainId << " = "//" = dso_local "
		if IsThreadLocal f << "thread_local "
		f << "global "
		if IsRef{
			ResultType.GetPoint().PrintType(f)
		}else{
			ResultType.PrintType(f)
		}
		if Down == null
		{
			if TypeContainVTable(ResultType)
			{
				if ResultType is TypeClass
				{
					PrintClassData(f,ResultType->{TypeClass^}.ToClass,true)
				}else{
					PrintArrData(f,ResultType,true)
				}
			}else{
				f << " zeroinitializer\n"
			}
		}else{
			f << " " << Down.GetName() << "\n"
		}
	}
}
GlobalParamVoid := class extend GlobalParam
{
	PrintPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
	}
	PrintUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
	}
	PrintPointPre := virtual !(TIOStream f, int newInd,int debId) -> void
	{
	}
	PrintPointUse := virtual !(TIOStream f, int newInd,int debId) -> void
	{
	}
	GetName := virtual !(int newInd) -> string
	{
		return ""
	}
	GetPointName := virtual !(int newInd) -> string
	{
		return ""
	}
}


globalVars := Queue.{Object^}
AddGlobalVar := !(Object^ val) -> void
{
	globalVars.Push(val)
}

PrintGlobalVars := !(TIOStream f) -> void
{
	globalVars[^].PrintGlobal(f)
}

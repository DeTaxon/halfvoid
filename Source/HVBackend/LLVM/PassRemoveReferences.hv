
PassRemoveReferences := !(MCFunction^ fnc) ->MCFunction^
{
	@once RetConstFuncTableInit()

	tbl := ref RetConstFuncTable

	newObjs := List.{MCObject}
	newCmds := List.{MCLine}

	newCmds.Push(fnc.Lines[^])
	newObjs.Push(fnc.Objects[^])

	newFunc := fnc.CreateBaseFunc()

	makeConst := (Type^ tp) ==> int
	{
		result = newObjs.Size()
		newObjs.Emplace()
		obj := ref newObjs[-1]
		obj->SetType(MCConstValue)
		obj&->{MCConstValue^}.GetValueType() = NormalizeType(tp)
	}

	vars := AVLMap.{int,int}

	getVar := (int id) ==> int
	{
		return vars[id]?

		tp := fnc.Objects[id].GetType()
		tp = tp.GetPoint()

		result = newObjs.Size()
		newObjs.Emplace()
		obj := ref newObjs[-1]
		obj->SetType(MCVariable)
		obj&->{MCVariable^}.GetValueType() = NormalizeType(tp)

		vars[id] = result
	}

	for it , i : newObjs
	{
		if it is not MCReference
			continue
		
		needCheck := false
		for ln : newCmds
		{
			for j : 5
				needCheck = needCheck or (ln.var1&[j] == i)
			if needCheck
				break
		}
		if not needCheck
			continue

		cnstId := newObjs.Size()
		newObjs.Emplace()

		newVar := ref newObjs[-1]&->{MCVariable^}^
		newVar->SetType(MCVariable)

		newVar.GetValueType() = it.GetType().GetPoint()

		indx := 0

		while indx < newCmds.Size()
		{
			op := ref newCmds[indx]

			varUsed := false

			for j : 5
				varUsed = varUsed or (op.var1&[j] == i)

			if varUsed
			{

				if (op.opcode == "load_element" or op.opcode == "llvm_getelementptr" or op.opcode == "llvm_getelementptr0") and op.var1 == i
				{
					tp := it.GetType()
					tp = tp.GetPoint()
					cnst := makeConst(tp)
					vr := getVar(op.var1)


					ln := MCLine("llvm_store",vr,cnst)
					newCmds.InsertAfter(indx,ln)

					if op.opcode != "llvm_getelementptr0"
						op.opcode = "llvm_getelementptr"
					op.var1 = cnst

					indx += 1
				}else{

					if (op.opcode == "load_element" or op.opcode == "llvm_getelementptr") and op.var2 == i {
						tp := fnc.Objects[op.var2].GetType()
						cnst := makeConst(tp.GetPoint())
						vr := getVar(op.var2)
						newCmds.InsertAfter(indx - 1,MCLine("llvm_load",cnst,vr))

						//printf("hm %p\n",fnc.Objects[op.var2].GetType() is in TypeClass,tp is in TypeClass)

						if tp is in TypeClass
						{
							op.opcode = "llvm_getelementptr0"
							op.var2 = cnst
						}else{
							cnst2 := makeConst(tp)
							newCmds.InsertAfter(indx,MCLine("llvm_load",cnst2,cnst))
							op.opcode = "llvm_getelementptr"
							op.var2 = cnst2
						}
					}

					if tbl.Contain(op.opcode) and op.var1 == i {
						tp := fnc.Objects[op.var1].GetType()

						cnstVal := makeConst(tp)
						cnstSet := makeConst(tp.GetPoint())
						vr := getVar(op.var1)

						op.var1 = cnstVal

						newCmds.InsertAfter(indx,MCLine("mov",cnstSet,vr))
						newCmds.InsertAfter(indx+1,MCLine("llvm_store",cnstSet,cnstVal))
					}
					if op.opcode == "get_pointer" and op.var2 == i {
						cnst := makeConst(fnc.Objects[op.var2].GetType().GetPoint())
						newCmds.InsertAfter(indx-1,MCLine("llvm_load",cnst,getVar(op.var2)))
						op.opcode = "cast"
						op.var2 = cnst
					}else if op.opcode == "mov" and op.var1 == i {

						ob1 := ref fnc.Objects[op.var1]
						ob2 := ref fnc.Objects[op.var2]

						if ob2 is MCVariable
						{
							tp := ob1.GetType()

							cnstVal := makeConst(tp)
							cnstSet := makeConst(tp.GetPoint())
							vr := getVar(op.var1)

							newCmds.InsertAfter(indx - 1,MCLine("mov",cnstSet,vr))
							op.opcode = "llvm_store"
							op.var1 = cnstSet

						}else{
							if not ob2.IsConstant and ob2 is not MCReference{
								printf("LLVM: failed to remove reference from mov at %i, %i %i\n",indx,op.var1,op.var2)
								assert(false)
							}
							tp := fnc.Objects[op.var1].GetType()

							cnstVal := makeConst(tp)
							cnstSet := makeConst(tp.GetPoint())
							vr := getVar(op.var1)

							newCmds.InsertAfter(indx - 1,MCLine("mov",cnstSet,vr))
							op.opcode = "llvm_store"
							op.var1 = cnstSet
						}

					}else{
						for j : 1..5
						{
							if op.var1&[j] != i
								continue
							tp := fnc.Objects[op.var1&[j]].GetType()

							cnst2 := makeConst(tp.GetPoint())
							cnst := makeConst(tp)
							vr := getVar(op.var1&[j])
							newCmds.InsertAfter(indx - 1,MCLine("llvm_load",cnst,cnst2))
							newCmds.InsertAfter(indx - 1,MCLine("llvm_load",cnst2,vr))
							op.var1&[j] = cnst
						}

					}
					indx += 1
				}

			}else{
				indx += 1
			}
		}
	}

	newFunc.Objects = newObjs.ToVector()
	newFunc.Lines = newCmds.ToVector()

	return newFunc
}



PassCreateConstValues := !(MCFunction^ fnc) ->MCFunction^
{
	// i should check if cast needed, but for 99.9% usage it will be true
	@once GenMCTable()
	tbl := ref OpcodeMustConstTable


	newObjs := List.{MCObject}
	newCmds := List.{MCLine}

	newObjs.Push(fnc.Objects[^])


	toConstVar := (MCObject obj) ==> int
	{
		result = newObjs.Size()
		newObjs.Push(obj)
		newObjs[-1]->SetType(MCConstValue)
	}
	makeConstVar := (int id) ==> int
	{
		obj := ref fnc.Objects[id]
		result = newObjs.Size()
		newObjs.Push(obj)
		newObjs[-1]->SetType(MCConstValue)
		newCmds.Push(MCLine("mov",result,id))
	}

	i := 0
	while i < fnc.Lines.Size()
	{
		it := ref fnc.Lines[i] //TODO iterator instead of O(n), or maybe generator for cycle
		switch it.opcode
		{
		case "mov" //TODO edge case

			ob1 := ref fnc.Objects[it.var1]
			ob2 := ref fnc.Objects[it.var2]

			if ob1 is MCVariable and ob2 is MCVariable
			{
				cnstId := toConstVar(ob2)
				newCmds.Push(MCLine("mov",cnstId,it.var2))
				newCmds.Push(MCLine("mov",it.var1,cnstId))
			}else{
				newCmds.Push(it)
			}
			i += 1

		case "call_start" // edge case, function pointers
			stLine := it
			i += 1

			varConsts := HybridQueue.{int,16}
			while fnc.Lines[i].opcode != "call_end"
			{
				vr := ref fnc.Lines[i]
				assert(vr.opcode == "load_var")

				obj := ref fnc.Objects[vr.var1]
				if obj is MCVariable
				{
					cnstId := toConstVar(obj)
					varConsts.Push(cnstId)
					newCmds.Push(MCLine("mov",cnstId,vr.var1))
				}else{
					varConsts.Push(vr.var1)
				}
				i += 1
			}
			newCmds.Push(stLine)
			newCmds.Push(MCLine("load_var",varConsts[^]))
			newCmds.Push(MCLine("call_end"))
			i += 1
		case void
			if tbl.Contain(it.opcode)
			{
				inf := ref tbl[it.opcode]
				if inf.opcode == "noop"
				{
					newCmds.Push(it)
				}else{
					//TODO
					
					vars := int[5]
					for i : 5
					{
						ob := ref fnc.Objects[it.var1&[i]]
						if inf.var1&[i] != 0 and ob is MCVariable
						{
							vars[i] = makeConstVar(it.var1&[i])
						}else{
							vars[i] = it.var1&[i]
						}
					}
					newCmds.Push(MCLine(it.opcode,vars[0],vars[1],vars[2],vars[3],vars[4]))
				}
			}else{
				printf("LLVM there is no const use in table for opcode %s\n",it.opcode.ToCStr())
				assert(false)
			}
			i += 1
		}
	}

	newFunc := fnc.CreateBaseFunc() 

	newFunc.Objects = newObjs.ToVector()
	newFunc.Lines = newCmds.ToVector()

	return newFunc
}

OpcodeMustConstTable := AVLMap.{MCBaseOpcodes,MCLine}

GenMCTable := !() -> void
{
	tbl := ref OpcodeMustConstTable
	//tbl["call_start"] = MCLine("noop")
	tbl["load_var"] = MCLine("noop",1)
	tbl["call_end"] = MCLine("noop")

	tbl["jmp"] = MCLine("noop")
	tbl["jmp_if_bool"] = MCLine("noop",0,1)
	tbl["label"] = MCLine("noop")

	tbl["get_pointer"] = MCLine("noop")

	tbl["cast"] = MCLine("noop",0,1)

	tbl["cmp"] = MCLine("noop",0,1,0,1)

	tbl["add"] = MCLine("noop",0,1,1)
	tbl["sub"] = MCLine("noop",0,1,1)
	tbl["mul"] = MCLine("noop",0,1,1)
	tbl["div"] = MCLine("noop",0,1,1)
	tbl["mod"] = MCLine("noop",0,1,1)

	tbl["memset"] = MCLine("noop",1,1,1)

	tbl["debug_set_file"] = MCLine("noop")
	tbl["debug_set_line"] = MCLine("noop")

	for it : tbl
	{
		if it.var1 != 0 or it.var2 != 0 or it.var3 != 0 or it.var4 != 0 or it.var5 != 0
			it.opcode = "mov"
	}
}

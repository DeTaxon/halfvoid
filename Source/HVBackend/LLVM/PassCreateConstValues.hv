

PassCreateConstValues := !(MCFunction^ fnc) ->MCFunction^
{
	// i should check if cast needed, but for 99.9% usage it will be true
	@once GenMCTable()
	tbl := ref OpcodeMustConstTable


	newObjs := List.{MCObject}
	newCmds := List.{MCLine}

	newObjs.Push(fnc.Objects[^])


	toConstVar := (MCObject obj) ==> int
	{
		result = newObjs.Size()
		newObjs.Emplace()
		newObjs[-1]->SetType(MCConstValue)
		newObjs[-1]&->{MCConstValue^}.GetValueType() = NormalizeType(obj.GetType())
	}
	makeConstVar := (int id) ==> int
	{
		obj := ref fnc.Objects[id]
		result = newObjs.Size()
		newObjs.Emplace()
		newObjs[-1]->SetType(MCConstValue)
		newObjs[-1]&->{MCConstValue^}.GetValueType() = NormalizeType(obj.GetType())
		newCmds.Push(MCLine("mov",result,id))
	}

	i := 0
	while i < fnc.Lines.Size()
	{
		it := ref fnc.Lines[i] //TODO iterator instead of O(n), or maybe generator for cycle
		switch it.opcode
		{
		case "mov" //TODO edge case

			ob1 := ref fnc.Objects[it.var1]
			ob2 := ref fnc.Objects[it.var2]

			if IsVarType(ob1) and IsVarType(ob2)
			{
				cnstId := toConstVar(ob2)
				newCmds.Push(MCLine("mov",cnstId,it.var2))
				newCmds.Push(MCLine("mov",it.var1,cnstId))
			}else{
				newCmds.Push(it)
			}
			i += 1

		case "call_start"
			stLine := it
			i += 1

			if IsVarType(fnc.Objects[stLine.var2])
			{
				cnstId := makeConstVar(stLine.var2)
				newCmds.Push(MCLine("mov",cnstId,stLine.var2))
				stLine.var2 = cnstId
			}

			varConsts := HybridQueue.{int,16}
			while fnc.Lines[i].opcode != "call_end"
			{
				vr := ref fnc.Lines[i]
				assert(vr.opcode == "load_var")

				obj := ref fnc.Objects[vr.var1]
				if IsObjectVariable(obj)
				{
					cnstId := toConstVar(obj)
					varConsts.Push(cnstId)
					newCmds.Push(MCLine("mov",cnstId,vr.var1))
				}else{
					varConsts.Push(vr.var1)
				}
				i += 1
			}
			newCmds.Push(stLine)
			newCmds.Push(MCLine("load_var",varConsts[^]))
			newCmds.Push(MCLine("call_end"))
			i += 1
		case "switch_start"
			stLine := it
			i += 1

			if IsVarType(fnc.Objects[stLine.var1])
			{
				cnstId := makeConstVar(stLine.var1)
				stLine.var1 = cnstId
			}
			newCmds.Push(stLine)
			while fnc.Lines[i].opcode != "switch_end"
			{
				newCmds.Push(fnc.Lines[i])
				i += 1
			}
			newCmds.Push(fnc.Lines[i])
			i += 1
		case "asm_start"
			stLine := it
			i += 1

			clobObjs := HybridQueue.{int,16}
			varConsts := HybridQueue.{int,16}
			while fnc.Lines[i].opcode != "asm_end"
			{
				vr := ref fnc.Lines[i]
				if vr.opcode == "load_var"
				{
					obj := ref fnc.Objects[vr.var1]
					if IsObjectVariable(obj)
					{
						cnstId := toConstVar(obj)
						varConsts.Push(cnstId)
						newCmds.Push(MCLine("mov",cnstId,vr.var1))
					}else{
						varConsts.Push(vr.var1)
					}
				}else if vr.opcode == "clob_str"{
					clobObjs.Push(vr.var1)
				}else{
					assert(false)
				}
				i += 1
			}
			newCmds.Push(stLine)
			for it : clobObjs
				newCmds.Push(MCLine("clob_str",it))
			for it : varConsts
				newCmds.Push(MCLine("load_var",it))
			newCmds.Push(MCLine("asm_end"))
			i += 1
		case void
			if tbl.Contain(it.opcode)
			{
				inf := ref tbl[it.opcode]

				thisLine := it

				if (thisLine.opcode == "load_element" or thisLine.opcode == "llvm_getelementptr")
					and IsVarType(fnc.Objects[thisLine.var2]) {

					ob1 := ref fnc.Objects[thisLine.var2]

					if ob1.GetType() is TypeClass {
						cnst := toConstVar(ob1)
						newCnst := ref newObjs[-1]&->{MCConstValue^}^
						newCnst.GetValueType() = ob1.GetType().GetPoint()

						newCmds.Push(MCLine("get_pointer",cnst,thisLine.var2))
						thisLine.opcode = "llvm_getelementptr0"
						thisLine.var2 = cnst
					}
				}

				if inf.opcode == "noop"
				{
					newCmds.Push(thisLine)
				}else{
					//TODO
					
					vars := int[5]
					for i : 5
					{
						ob := ref fnc.Objects[thisLine.var1&[i]]
						if inf.var1&[i] != 0 and IsObjectVariable(ob)
						{
							vars[i] = makeConstVar(thisLine.var1&[i])
						}else{
							vars[i] = thisLine.var1&[i]
						}
					}
					newCmds.Push(MCLine(thisLine.opcode,vars[0],vars[1],vars[2],vars[3],vars[4]))
				}
			}else{
				printf("LLVM there is no const use in table for opcode %s\n",it.opcode.ToCStr())
				assert(false)
			}
			i += 1
		}
	}

	newFunc := fnc.CreateBaseFunc() 

	newFunc.Objects = newObjs.ToVector()
	newFunc.Lines = newCmds.ToVector()

	return newFunc
}

OpcodeMustConstTable := AVLMap.{MCBaseOpcodes,MCLine}


IsVarType := !(MCObject obj) -> bool
{
	if obj is MCVariable
		return true
	if obj is MCGlobalVariable
		return true
	return false
}
GenMCTable := !() -> void
{
	tbl := ref OpcodeMustConstTable
	//tbl["call_start"] = MCLine("noop")
	tbl["load_var"] = MCLine("noop",1)
	tbl["call_end"] = MCLine("noop")

	tbl["jmp"] = MCLine("noop")
	tbl["jmp_if_bool"] = MCLine("noop",0,1)
	tbl["label"] = MCLine("noop")

	tbl["load_element"] = MCLine("noop",0,1,1,1,1)
	tbl["get_pointer"] = MCLine("noop")

	tbl["cast"] = MCLine("noop",0,1)

	tbl["cmp"] = MCLine("noop",0,1,0,1)

	tbl["and"] = MCLine("noop",0,1,1)
	tbl["or"] = MCLine("noop",0,1,1)
	tbl["xor"] = MCLine("noop",0,1,1)
	tbl["not"] = MCLine("noop",0,1)

	tbl["shl"] = MCLine("noop",0,1,1)
	tbl["sshr"] = MCLine("noop",0,1,1)
	tbl["ushr"] = MCLine("noop",0,1,1)

	tbl["add"] = MCLine("noop",0,1,1)
	tbl["sub"] = MCLine("noop",0,1,1)
	tbl["mul"] = MCLine("noop",0,1,1)
	tbl["div"] = MCLine("noop",0,1,1)
	tbl["mod"] = MCLine("noop",0,1,1)

	tbl["atomic_add"] = MCLine("noop",0,0,1)
	tbl["atomic_sub"] = MCLine("noop",0,0,1)
	tbl["atomic_xchg"] = MCLine("noop",0,0,1)

	tbl["memset"] = MCLine("noop",1,1,1)
	tbl["clean"] = MCLine("noop")

	tbl["max"] = MCLine("noop",0,1,1)
	tbl["min"] = MCLine("noop",0,1,1)


	tbl["debug_set_file"] = MCLine("noop")
	tbl["debug_set_line"] = MCLine("noop")

	tbl["llvm_store"] = MCLine("noop",0,1)
	tbl["llvm_load"] = MCLine("noop")
	tbl["llvm_getelementptr"] = MCLine("noop",0,1,1,1,1)
	tbl["llvm_getelementptr0"] = MCLine("noop",0,1,1,1,1)

	tbl["asm_start"] = MCLine("noop")
	tbl["asm_end"] = MCLine("noop")

	for it : tbl
	{
		if it.var1 != 0 or it.var2 != 0 or it.var3 != 0 or it.var4 != 0 or it.var5 != 0
			it.opcode = "mov"
	}
}

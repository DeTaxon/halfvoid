
hvr_Class := class extend BoxClassPoison
{
	newFieldCB := !(StringView)&->void^

	this := !() -> void
	{
		Name = "ModuleClass" //todo
		ItId = GetNewId()
		ItType = new TypeClass(this&)

		ItAlign = 1 //TODO pointer size
		ItSize = 0
	}
	GetNewFieldType := virtual !(StringSpan val) -> Type^
	{
		if newFieldCB != null
			return newFieldCB?(val)?->{ConstType^}.Value
		return null
	}
	GetField := virtual !(StringSpan val) -> FieldPath^
	{
		for it : FieldsPath
		{
			if it.Field.Name == val
			{
				return it
			}
		}

		newFieldType := GetNewFieldType(val)

		if newFieldType == null
			return null

		newField := new FieldParam(val,newFieldType)
		Fields.Push(newField)

		fPath := new FieldPath(ItType,newField.Value)
		fPath.Field = newField
		fPath.Index = FieldsPath.Size()

		FieldsPath.Push(fPath)

		fieldAlign := newFieldType.GetAlign()
		ItAlign = max(ItAlign,fieldAlign)

		unalign := ItSize mod fieldAlign
		if unalign != 0
		{
			ItSize += fieldAlign - unalign
		}

		ItSize += newFieldType.GetSize() //todo variable size object?
		RecalculateSize()
		//printf("hm %i\n",ItSize)

		for it : fieldIterators
		{
			it.ItemCreated(FieldsPath.Size() - 1)
		}

		return fPath 
	}

}

HVClassImpl := class extend HV_Class
{
	baseClass := hvr_Class^

	AddField := virtual !(Str fieldName,void^ fieldType) -> void
	{
		if fieldType == null
			return void
		obj := fieldType->{ConstType^}
		spn := GetConstString(fieldName.AsStringSpan())
		baseClass.AddExtraField(spn,obj.Value)
	}
	AddCallback_GetFieldFailed := virtual !(!(StringView)&->void^ cb ) -> void
	{
		baseClass.newFieldCB = null
		baseClass.newFieldCB = cb?.Capture()
	}
	GetType := virtual !() -> void^ 
	{
		return GetConstType(baseClass.ItType)
	}
	AddMethod := virtual !(Str funcName,void^ fncObj) -> void
	{
		if fncObj == null
			return void
		if fncObj->{BoxUnit^} is not in BoxFuncTemplate
			return void
		spn := GetConstString(funcName.AsStringSpan())
		tmpl := fncObj->{BoxFuncTemplateBody^} //TODO, check if body
		tmpl.ParentScope = baseClass
		baseClass.DefaultSpace.MethodTemplates[spn].Push(fncObj->{BoxFuncTemplate^})
	}
	AddMethodStr := virtual !(Str funcName,Str fncText) -> void
	{
		oldScope := CurrentScope
		defer CurrentScope = oldScope
		CurrentScope = baseClass

		fnc := StrToFunctionTemplate(fncText)
		if fnc == null
			return void
		spn := GetConstString(funcName.AsStringSpan())
		baseClass.DefaultSpace.MethodTemplates[spn].Push(fnc)
	}
}

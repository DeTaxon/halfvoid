
BW := class
{
	sPoint := u8^
	byteOffset := int
	bitOffset := int
	endSize := int

	this := !(void^ data, int size) -> void 
	{
		sPoint = data->{u8^}
		byteOffset = 0
		bitOffset = 0
		endSize = size
	}
	Move := !(int toMov) -> void 
	{
		bitOffset += toMov
		byteOffset += bitOffset div 8
		bitOffset = bitOffset mod 8
	}
	Get := !(int siz) -> int 
	{
		msk := 0
		msk = not_b msk
		msk = msk shl siz
		msk = not_b msk
		preRes := 0
		c := byteOffset + 4
		while c >= byteOffset
		{
			preRes = preRes shl 8
			preRes += sPoint[c]->{u32}->{int}
			c--
		}
		preRes = preRes shr bitOffset
		return preRes and_b msk
	}
	MoveUp := !() -> void
	{
		if bitOffset != 0
		{
			bitOffset = 0
			byteOffset += 1
		}
	}
	CopyTo := !(u8^  toSet,int blkSize) -> void
	{
		memcpy(toSet,sPoint[byteOffset]&,blkSize)
		byteOffset += blkSize
	}
	GetAndMove := !(int siz) -> int
	{
		preRes := Get(siz)
		Move(siz)
		return preRes
	}
}



NotHuffmanTree9 := class  extend TGCObject
{
	treeInfo := Tuple.{s16,u8}[512] // character, size
	extraNodes := Vector.{Tuple.{u16, u16}}[16] //charact,value [bitsize]

	GetAndMove := !(BW bw) -> int
	{
		itVal := bw.Get(9)
		inFast := treeInfo[itVal]&

		if inFast.0 != -1 {
			bw.Move(inFast.1)
			return inFast.0
		}
		for i : 10..15
		{
			if extraNodes[i].Size() == 0
				continue
			itVal = bw.Get(i)
			if extraNodes[i][^].0 == itVal
			{
				bw.Move(i)
				return it.1
			}
		}
		return -1
	}
}

inverseSomeBit := !(int inp,int size) -> int
{
	preRes := 0
	for size
	{
		preRes = preRes shl 1
		preRes += inp and_b 1
		inp = inp shr 1
	}
	return preRes
}

ApplyHuffmanTree := !(char^ valueSizes,int arrLen, NotHuffmanTree9^ toSet) -> void
{
	maxSize := 0
	for c : arrLen
	{
		if maxSize < valueSizes[c]
			maxSize = valueSizes[c]
	}
	blCount := Vector.{int}(maxSize+1)

	for i : arrLen
		blCount[valueSizes[i]]++
	blCode := Vector.{int}(maxSize + 1)
	blCount[0] = 0
	for it : toSet.treeInfo
		it.0 = -1


	code := 0
	for i : 1..maxSize
	{
		nxCode := code + blCount[i-1]
		code = nxCode shl 1
		blCode[i] = code
	}
	resCodes := Vector.{int}(arrLen)
	for i : arrLen
	{
		c := valueSizes[i]
		if c != 0
		{
			resCodes[i] = blCode[c]
			blCode[c]++
		}
	}
	S := 9
	for c,i : resCodes
	{
		valSize := valueSizes[i]
		if  valueSizes[i] <= S
		{
			if valSize == 0 continue
			diff := S - valSize
			toPut := inverseSomeBit(c,valSize)
			for j : 0..((1 shl diff) - 1)
			{
				preSet := toPut + (j shl valSize)
				ts :=  toSet.treeInfo[preSet]&
				ts.0 = i
				ts.1 = valSize
			}
		}
	}
	
	if maxSize > S
	{
		memItr := int[16]
		for it : memItr
			it = 0

		for i : (S+1)..maxSize
		{
			if blCount[i] != 0
			{
				ZeroMem(toSet.extraNodes[i]) //TODO array of VTable types does not work
				toSet.extraNodes[i].Resize(blCount[i])
				//toSet.extraNodes[i] = new Tuple.{u16,u16}[blCount[i]]
			}
		}
		for i : arrLen
		{
			it := valueSizes[i]
			if it <= S continue
			apl := toSet.extraNodes[it][memItr[it]]&
			apl.0 = inverseSomeBit(resCodes[i],it)
			apl.1 = i
			memItr[it]++
		}
	}
}
DecodeSmallHuffmanTable := !(NotHuffmanTree9^ toApply,BW bw,u8^ res,int codesCount) -> void
{
	prevCode := 0
	outIter := res->{u8^}
	while codesCount != 0
	{
		itVal := toApply.GetAndMove(bw)	
		switch itVal
		{
			case in 0..15
				prevCode = itVal
				outIter^ =itVal
				outIter = outIter[1]&
				codesCount--
			case 16
				c1 := bw.GetAndMove(2)
				for c1+3
				{
						outIter^ = prevCode
						outIter = outIter[1]&
				}
				codesCount -= c1+3
			case 17
				c2 := bw.GetAndMove(3)
				for c2+3
				{
					outIter^ = 0
					outIter = outIter[1]&
				}
				codesCount -= c2+3
			case 18
				c3 := bw.GetAndMove(7)
				for c3+11
				{
					outIter^ = 0
					outIter = outIter[1]&
				}
				codesCount -= c3+11
		}
	}
}


fixedHuffmanLiteral := NotHuffmanTree9^
fixedHuffmanDistance := NotHuffmanTree9^

isInitedFixed := bool
InitFixedHuffman := !() -> void
{
	litLens := char[2897] 
	for 0..143 litLens[it] = 8
	for 144..255 litLens[it] = 9
	for 256..279 litLens[it] = 7
	for 280..287 litLens[it] = 8

	fixedHuffmanLiteral = new NotHuffmanTree9
	ApplyHuffmanTree(litLens,287,fixedHuffmanLiteral)

	dists := char[32]
	dists[^] = 5
	fixedHuffmanDistance = new NotHuffmanTree9
	ApplyHuffmanTree(dists,32,fixedHuffmanDistance)

	isInitedFixed = true
}



codeLenTable := ![16,17,18,0,8,7,9,6,10,5,11,4,12,3,13,2,14,1,15]
codeLenExtraBits := ![0,0,0,0,0,0,0, 0, 1, 1, 1, 1, 2, 2, 2, 2, 3, 3, 3, 3, 4, 4, 4,  4,  5,  5,  5,  5,  0]
codeLenExtraBias := ![3,4,5,6,7,8,9,10,11,13,15,17,19,23,27,31,35,43,51,59,67,83,99,115,131,163,195,227,258] 
codeDistBits := ![0,0,0,0,1,1,2,2,3,3,4,4,5,5,6,6,7,7,8,8,9,9,10,10,11,11,12,12,13,13]
codeDistBias := ![1,2,3,4,5,7,9,13,17,25,33,49,65,97,129,193,257,385,513,769,1025,1537,2049,3073,4097,6145,8193,12289,16385,24577]
CrappyDeflateInflate := !(void^ inpPoint,size_t inpSize,u8^ outpPoint,size_t outpSize) -> bool
{
	bw := BW(inpPoint,inpSize)

	while true
	{
		isEnd := bw.GetAndMove(1)
		compType := bw.GetAndMove(2)


		if compType == 0
		{
		  	bw.MoveUp()
			toCpy := bw.Get(16)
			bw.Move(32)
			bw.CopyTo(outpPoint,toCpy)
			outpPoint = outpPoint[toCpy]&
		}else{
			litsTree := NotHuffmanTree9^
			distTree := NotHuffmanTree9^

			if compType == 1
			{
				if not isInitedFixed
					InitFixedHuffman()
				litsTree = fixedHuffmanLiteral
				distTree = fixedHuffmanDistance
			}else{
				HLIT := bw.GetAndMove(5) + 257
				HDIST := bw.GetAndMove(5) + 1
				HCLEN := bw.GetAndMove(4) + 4

				codeLens := char[19]

				for it : HCLEN
				{
					codeLens[codeLenTable[it]] = bw.GetAndMove(3)
				}

				newTree := new NotHuffmanTree9
				ApplyHuffmanTree(codeLens[0]&,19,newTree)

				resultData := Vector.{u8}(HLIT + HDIST)
				DecodeSmallHuffmanTable(newTree,bw,resultData.GetPointer(),resultData.Size())

				litsTree = new NotHuffmanTree9
				distTree = new NotHuffmanTree9
				
				ApplyHuffmanTree(resultData.GetPointer()->{char^},HLIT,litsTree)
				ApplyHuffmanTree(resultData[HLIT]&->{char^},HDIST,distTree)
			}

			while true
			{
				itVal := litsTree.GetAndMove(bw)

				if itVal == -1 return false

				if itVal == 256
					break
				if itVal >= 256 
				{
					toAdd := bw.GetAndMove(codeLenExtraBits[itVal-257])
					toAdd += codeLenExtraBias[itVal-257]
					ct := distTree.GetAndMove(bw)

					dist := codeDistBias[ct]
					dist += bw.GetAndMove(codeDistBits[ct])
					
					cpyIter := outpPoint[-dist]&
					for toAdd 
					{
						outpPoint^ = cpyIter^
						outpPoint = outpPoint[1]&
						cpyIter = cpyIter[1]&
					}
					//return true
				}else{
					outpPoint^ = itVal
					outpPoint = outpPoint[1]&
				}
			}
		}
		if isEnd == 1
			return true
	}

	return true
}

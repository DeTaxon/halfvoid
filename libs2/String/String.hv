String := class extend StringView
{
	charPtr := char^
	itSize := int

	this := !(char^ inp) -> void
	{
		if inp == null
		{
			charPtr = null
			itSize = 0
		}else{
			itSize = strlen(inp)
			charPtr = malloc(itSize + 1)->{char^}
			memcpy(charPtr,inp,itSize)
			charPtr[itSize] = 0
		}
	}
	this := !(char^ inp, int size) -> void
	{
		itSize = size
		charPtr = malloc(itSize + 1)->{char^}
		memcpy(charPtr,inp,itSize)
		charPtr[itSize] = 0
	}
	Destroy := virtual !() -> void
	{
		if charPtr == null
			return void
		
		free(charPtr)
		charPtr = null
		itSize = 0
	}
	"[]" := !(int ind) -> int
	{
		assert(ind >= 0 and ind < itSize)
		return charPtr[ind]
	}
	Str := virtual !() -> char^
	{
		return charPtr
	}
	Size := virtual !() -> int
	{
		return itSize
	}

	GetString := virtual !() -> String^
	{
		return this&
	}
	Size := !() -> int { return itSize }
}

StringViewOnCStr := class extend StringView
{
	ptrBase := char^
	itSize := int
	this := !(char^ inp) -> void { ptrBase = inp  itSize = strlen(inp)}
	this := !(char^ inp,int size) -> void { ptrBase = inp  itSize = size}
	Str := virtual !() -> char^ { return ptrBase }
	Size := virtual !() -> int { return itSize }
	GetString := virtual !() -> String^ { return new String(ptrBase,itSize) }
}


"->{}" := !(char^ obj) .{StringView} -> StringViewOnCStr
{
	result.ptrBase = obj
	result.itSize = strlen(obj)
}

//"->{}" := !(TString^ st) .{char^} -> char^
//{
//	return st.Str()
//}

"<=>" := !(char^ a, char^ b) -> int
{
	if a->{void^} == null and b->{void^} == null
		return 0
	if a->{void^} == null
		return -1
	if b->{void^} == null
		return 1
	return strcmp(a,b)
}
"!=" := !(char^ a,char^ b) -> bool
{
	cmp := a <=> b
	result = cmp != 0
}
"==" := !(char^ a, char^ b) -> bool
{
	cmp := a <=> b
	result = cmp == 0
}

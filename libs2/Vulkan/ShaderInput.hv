AppendClass VulkanInstance
{
	CreateLayout := !(VKType[] vInput,TShaderSet^[] sets, TPushConstant^[] cnsts, int resImgCount) -> TShaderInput^
	{
		result = new TShaderInput
		result.itInstance = this&
		result.CreateInput(vInput,sets,cnsts,resImgCount)
	}
}


TShaderInput := class 
{
	itInstance := VulkanInstance^
	itPass := TRenderPass^
	itLayout := VkPipelineLayout

	vertexInput := HybridQueue.{VKType,6}
	itSets := Vector.{TShaderSet^}
	resultImageCount := int

	Get := !() -> VkPipelineLayout
	{
		return itLayout
	}

	CreateInput := !(VKType[] vInput,TShaderSet^[] sets,TPushConstant^[] cnsts,int outputImageCount) -> void
	{
		resultImageCount = outputImageCount

		for it : vInput
			vertexInput.Push(it)
		for it : sets
			it.Create(itInstance)

		itSets.Resize(sets.Size())
		for it,i : sets
			itSets[i] = it

		assert(sets.Size() <= 10)
		layouts := void^[10]
		layouts[i] = sets[^i].Get()

		cnstsPtr := void^
		cnstsVec := Vector.{VkPushConstantRange}

		if cnsts.Size() != 0
		{
			if cnsts.Size() == 1
			{
				cnstsPtr = cnsts[0].Get()
			}else{
				cnstsVec.Resize(cnsts.Size())
				cnstsPtr = cnstsVec.GetPointer()
				for i : cnsts.Size()
				{
					cnstsVec[i].stageFlags = cnsts[i].Get().stageFlags
					cnstsVec[i].offset = cnsts[i].Get().offset
					cnstsVec[i].size = cnsts[i].Get().size
				}
			}
		}
	
		ppC := VkPipelineLayoutCreateInfo()
		ppC.pushConstantRangeCount = cnsts.Size()
		ppC.pPushConstantRanges&->{void^^}^ = cnstsPtr->{void^}
		ppC.setLayoutCount = sets.Size()
		ppC.pSetLayouts&->{void^^}^ = layouts&->{void^}
	
		itInstance.Funcs.vkCreatePipelineLayout(vkLogCard,ppC&,null,itLayout&)
	}
	CreateDescriptorPool := !(int setIndex,int poolSize) -> TDescriptorPool^
	{
		return new TDescriptorPool(this&,setIndex,poolSize)
	}

	CreateShader := !(vRepoFile^ vertBlob, vRepoFile^ fragBlob) -> vShader^
	{
		mp1 := vertBlob.GetMap()
		b1 := new BlobOnVector(mp1.Size())
		if mp1.Size() != 0
			memcpy(b1.GetPointer(),mp1.Get(),mp1.Size())
		mp2 := fragBlob.GetMap()
		b2 := new BlobOnVector(mp2.Size())
		if mp2.Size() != 0
			memcpy(b2.GetPointer(),mp2.Get(),mp2.Size())
		return CreateShader(b1,b2)
	}
	CreateShader := !(Blob^ vertBlob, Blob^ fragBlob) -> vShader^
	{
		vrtObj := LoadShaderModule3(vertBlob)
		fragObj := LoadShaderModule3(fragBlob)

		vert := vrtObj.Get()
		frag := fragObj.Get()

		sStg := VkPipelineShaderStageCreateInfo[2]
		sStg[^].sType = VK_STRUCTURE_TYPE_PIPELINE_SHADER_STAGE_CREATE_INFO

		sStg[0].stage = VK_SHADER_STAGE_VERTEX_BIT
		sStg[0].module = vert
		sStg[0].pName&->{void^^}^ = "main"
		sStg[1].stage = VK_SHADER_STAGE_FRAGMENT_BIT
		sStg[1].module = frag
		sStg[1].pName&->{void^^}^ = "main"


		inputsBind := VkVertexInputBindingDescription
		inputsBind.binding = 0
		inputsBind.inputRate = VK_VERTEX_INPUT_RATE_VERTEX

		assert(vertexInput.Size() <= 10)
		offset := 0
		inputsStuf := VkVertexInputAttributeDescription[10]

		for i : vertexInput.Size() //TODO for it,i
		{
			it := vertexInput[i]

			inputsStuf[i].location = i
			inputsStuf[i].binding = inputsBind.binding
			inputsStuf[i].format = it.GetVKType()
			inputsStuf[i].offset = offset
			offset += it.GetSize()
		}
	
		inputsBind.stride = offset

		plC := VkPipelineVertexInputStateCreateInfo()

		plC.vertexBindingDescriptionCount = 1
		plC.pVertexBindingDescriptions&->{void^^}^ = inputsBind&
		plC.vertexAttributeDescriptionCount = vertexInput.Size()
		plC.pVertexAttributeDescriptions&->{void^^}^ = inputsStuf[0]&

		paC1 := VkPipelineInputAssemblyStateCreateInfo()
		paC1.topology = VK_PRIMITIVE_TOPOLOGY_TRIANGLE_LIST

		stts := VkDynamicState[2]
		stts[0] = VK_DYNAMIC_STATE_VIEWPORT
		stts[1] = VK_DYNAMIC_STATE_SCISSOR

		dins := VkPipelineDynamicStateCreateInfo()
		dins.dynamicStateCount = 2
		dins.pDynamicStates = stts[0]&

		pVSC := VkPipelineViewportStateCreateInfo()
		pVSC.viewportCount = 1
		pVSC.pViewports = null
		pVSC.scissorCount = 1
		pVSC.pScissors = null

		rasterC := VkPipelineRasterizationStateCreateInfo()
		rasterC.polygonMode = VK_POLYGON_MODE_FILL
		rasterC.cullMode = VK_CULL_MODE_BACK_BIT
		rasterC.cullMode = VK_CULL_MODE_NONE
		rasterC.frontFace = VK_FRONT_FACE_COUNTER_CLOCKWISE
		rasterC.lineWidth = 1.0f
		//  VK_FALSE, VkBool32   depthClampEnable
		//  VK_FALSE, VkBool32   rasterizerDiscardEnable
		//  VK_FALSE, VkBool32   depthBiasEnable
		//  0.0f,     float      depthBiasConstantFactor
		//  0.0f,     float      depthBiasClamp
		//  0.0f,     float      depthBiasSlopeFactor

		sampleC := VkPipelineMultisampleStateCreateInfo()
		sampleC.rasterizationSamples = VK_SAMPLE_COUNT_1_BIT
		sampleC.minSampleShading = 1.0f
		//  VK_FALSE,   VkBool32              sampleShadingEnable
		//  nullptr,    const VkSampleMask   *pSampleMask
		//  VK_FALSE,   VkBool32              alphaToCoverageEnable
		//  VK_FALSE    VkBool32              alphaToOneEnable

		assert(resultImageCount <= 4)

		blndPre := VkPipelineColorBlendAttachmentState[4]

		for i : resultImageCount
		{
			blndPre[i].blendEnable = VK_TRUE //TODO PERFOMANCE
			blndPre[i].srcColorBlendFactor = VK_BLEND_FACTOR_SRC_ALPHA
			blndPre[i].dstColorBlendFactor = VK_BLEND_FACTOR_ONE_MINUS_SRC_ALPHA
			blndPre[i].colorBlendOp = VK_BLEND_OP_ADD
			blndPre[i].srcAlphaBlendFactor = VK_BLEND_FACTOR_ONE
			blndPre[i].dstAlphaBlendFactor = VK_BLEND_FACTOR_ZERO
			blndPre[i].alphaBlendOp = VK_BLEND_OP_ADD
			blndPre[i].colorWriteMask = VK_COLOR_COMPONENT_R_BIT or_b VK_COLOR_COMPONENT_G_BIT or_b VK_COLOR_COMPONENT_B_BIT or_b VK_COLOR_COMPONENT_A_BIT

	
			//TODO this code runs faster, but sometime i need alpha blending
			//blndPre[i].srcColorBlendFactor = VK_BLEND_FACTOR_ONE
			//blndPre[i].dstColorBlendFactor = VK_BLEND_FACTOR_ZERO
			//blndPre[i].colorBlendOp = VK_BLEND_OP_ADD
			//blndPre[i].srcAlphaBlendFactor = VK_BLEND_FACTOR_ONE
			//blndPre[i].dstAlphaBlendFactor = VK_BLEND_FACTOR_ZERO
			//blndPre[i].alphaBlendOp = VK_BLEND_OP_ADD
			//blndPre[i].colorWriteMask = VK_COLOR_COMPONENT_R_BIT or_b VK_COLOR_COMPONENT_G_BIT or_b VK_COLOR_COMPONENT_B_BIT or_b VK_COLOR_COMPONENT_A_BIT
		}


		blndC := VkPipelineColorBlendStateCreateInfo()
		blndC.logicOp = VK_LOGIC_OP_COPY
		blndC.attachmentCount = resultImageCount
		blndC.pAttachments&->{void^^}^ = blndPre[0]&
		//  VK_FALSE,                     VkBool32   logicOpEnable
		//  { 0.0f, 0.0f, 0.0f, 0.0f }    float      blendConstants[4]

		pLC := VkPipelineLayoutCreateInfo()
		//  0,        VkPipelineLayoutCreateFlags    flags
		//  0,        uint32_t                       setLayoutCount
		//  nullptr,  const VkDescriptorSetLayout   *pSetLayouts
		//  0,        uint32_t                       pushConstantRangeCount
		//  nullptr   const VkPushConstantRange     *pPushConstantRanges

		dsC := VkPipelineDepthStencilStateCreateInfo()
		dsC.depthTestEnable = VK_TRUE
		dsC.depthWriteEnable = VK_TRUE
		dsC.depthCompareOp = VK_COMPARE_OP_LESS_OR_EQUAL
		dsC.depthBoundsTestEnable = VK_FALSE
		dsC.minDepthBounds = 0.0f
		dsC.maxDepthBounds = 0.0f
		dsC.stencilTestEnable = VK_FALSE
		dsC.back.failOp = VK_STENCIL_OP_KEEP
		dsC.back.passOp = VK_STENCIL_OP_KEEP
		dsC.back.compareOp = VK_COMPARE_OP_ALWAYS
		dsC.back.compareMask = 0
		dsC.back.reference = 0
		dsC.back.depthFailOp = VK_STENCIL_OP_KEEP
		dsC.back.writeMask = 0
		dsC.front.failOp = VK_STENCIL_OP_KEEP
		dsC.front.passOp = VK_STENCIL_OP_KEEP
		dsC.front.compareOp = VK_COMPARE_OP_ALWAYS
		dsC.front.compareMask = 0
		dsC.front.reference = 0
		dsC.front.depthFailOp = VK_STENCIL_OP_KEEP
		dsC.front.writeMask = 0


		gpC := VkGraphicsPipelineCreateInfo()
		gpC.stageCount = 2
		gpC.pStages&->{void^^}^ = sStg[0]&
		gpC.pVertexInputState&->{void^^}^ = plC&
		gpC.pInputAssemblyState&->{void^^}^ = paC1&
		gpC.pViewportState&->{void^^}^ = pVSC&
		gpC.pRasterizationState&->{void^^}^ = rasterC&
		gpC.pMultisampleState&->{void^^}^ = sampleC&
		gpC.pColorBlendState&->{void^^}^ = blndC&
		gpC.layout = this.Get()
		gpC.renderPass = itPass.Get()
		gpC.basePipelineIndex = -1
		gpC.pDepthStencilState&->{void^^}^ = dsC&
		gpC.pDynamicState&->{void^^}^ = dins&
		//  nullptr,        const VkPipelineTessellationStateCreateInfo   *pTessellationState
		//  nullptr,        const VkPipelineDynamicStateCreateInfo        *pDynamicState
		//  0,              uint32_t                                       subpass
		//  VK_NULL_HANDLE, VkPipeline                                     basePipelineHandle

		result = new vShader
		itInstance.Funcs.vkCreateGraphicsPipelines(vkLogCard,null,1,gpC&,null,result.itPipe&)
	}
}
LoadShaderModule2 := !(vRepoFile^ fil) -> vShaderModule^
{
	filMap := fil.GetMap()

	result = new vShaderModule
	result.LoadShaderModule(filMap.Get(),filMap.Size())
}
LoadShaderModule3 := !(Blob^ fil) -> vShaderModule^
{
	result = new vShaderModule
	result.LoadShaderModule(fil.GetPointer(),fil.Size())
}

TShaderSet := class extend TGCObject
{
	itSet := VkDescriptorSetLayout
	Get := !() -> VkDescriptorSetLayout
	{
		return itSet
	}

	Create := virtual !(VulkanInstance^ inst) -> void
	{
	}

	GetPoolType := virtual !() -> int
	{
		assert(false)
	}
	GetPoolScale := virtual !() -> int { return 1}
}
TUniformSet := class extend TShaderSet
{
	this := !() -> void
	{
	}

	Create := virtual !(VulkanInstance^ inst) -> void
	{
		oneDesc := VkDescriptorSetLayoutBinding
		oneDesc.binding = 0
		oneDesc.descriptorType = VK_DESCRIPTOR_TYPE_UNIFORM_BUFFER
		oneDesc.descriptorCount = 1
		oneDesc.stageFlags = VK_SHADER_STAGE_VERTEX_BIT
		oneDesc.pImmutableSamplers = null
	
		setDesc := VkDescriptorSetLayoutCreateInfo()
		setDesc.bindingCount = 1
		setDesc.pBindings&->{void^^}^ = oneDesc&->{void^}
	
		inst.Funcs.vkCreateDescriptorSetLayout(vkLogCard,setDesc&,null,itSet&)
	}
	GetPoolType := virtual !() -> int
	{
		return VK_DESCRIPTOR_TYPE_UNIFORM_BUFFER
	}
}
TImageSet := class extend TShaderSet
{
	imgCount := int
	this := !(int imageCount = 1) -> void
	{
		imgCount = imageCount
	}
	Create := virtual !(VulkanInstance^ inst) -> void
	{  
		assert(imgCount <= 10)
		twoDesc := VkDescriptorSetLayoutBinding[10]

		for i : imgCount
		{
			twoDesc[i].binding = i
			twoDesc[i].descriptorType = VK_DESCRIPTOR_TYPE_COMBINED_IMAGE_SAMPLER
			twoDesc[i].descriptorCount = 1
			twoDesc[i].stageFlags = VK_SHADER_STAGE_FRAGMENT_BIT
			twoDesc[i].pImmutableSamplers = null
		}
	
		set2Desc := VkDescriptorSetLayoutCreateInfo()
		set2Desc.bindingCount = imgCount
		set2Desc.pBindings&->{void^^}^ = twoDesc[0]&->{void^}
	
		inst.Funcs.vkCreateDescriptorSetLayout(vkLogCard,set2Desc&,null,itSet&)
	}
	GetPoolType := virtual !() -> int
	{
		return VK_DESCRIPTOR_TYPE_COMBINED_IMAGE_SAMPLER
	}
	GetPoolScale := virtual !() -> int { return imgCount}
}

TPushConstant := class extend TGCObject
{
	cnst := VkPushConstantRange
	Get := !() -> VkPushConstantRange^
	{
		return cnst&
	}
}
TVertexPushConstant := class extend TPushConstant
{
	this := !(int itSize) -> void
	{
		cnst.stageFlags = VK_SHADER_STAGE_VERTEX_BIT
		cnst.offset = 0
		cnst.size = itSize
	}
}

TDescriptorPool := class extend TGCObject
{
	baseLayout := TShaderInput^
	vPool := VkDescriptorPool
	itSet := TShaderSet^
	this := !(TShaderInput^ itm,int setIndex,int poolSize) -> void
	{
		//descPoolSize := VkDescriptorPoolSize
		//descPoolSize.descriptorCount = poolSize
		////descPoolSize.type = VK_DESCRIPTOR_TYPE_UNIFORM_BUFFER //TODO
		//descPoolSize.0 = VK_DESCRIPTOR_TYPE_UNIFORM_BUFFER

		baseLayout = itm
		itSet = itm.itSets[setIndex]
	
		test := u32[2]
		test[0] = itSet.GetPoolType()
		test[1] = poolSize*itSet.GetPoolScale() //count
	
		descPoolC := VkDescriptorPoolCreateInfo()
		descPoolC.maxSets = poolSize
		descPoolC.poolSizeCount = 1
		descPoolC.pPoolSizes&->{void^^}^ = test[0]&->{void^}//descPoolSize
	
		baseLayout.itInstance.Funcs.vkCreateDescriptorPool(vkLogCard,descPoolC&,null,vPool&)
	}
	CreateDescriptor := !() -> VkDescriptorSet
	{
		lay := itSet.Get()
		allDesc := VkDescriptorSetAllocateInfo()
		allDesc.descriptorPool = vPool
		allDesc.descriptorSetCount = 1
		allDesc.pSetLayouts&->{void^^}^ = lay&

		preRes := void^
		baseLayout.itInstance.Funcs.vkAllocateDescriptorSets(vkLogCard,allDesc&,preRes&)
		return preRes
	}
	//TODO need Destroy
}

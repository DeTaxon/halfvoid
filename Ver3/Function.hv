TryParseFunction := !(Token^ obj) -> BoxFunc^
{
	Attr := FuncAttributes

	if obj == null or obj.Down == null return null

	iter := obj.Down

	switch iter.GetValue() //TODO to cycle
	{
		case "virtual"
			Attr.IsVirtual = true
			iter = iter.Right
			if iter == null
				return null
		case void
	}

	if iter.GetValue() != "!"
		return null
	
	iter = iter.Right
	if iter == null return null

	if iter.GetValue() != "()"
		return null

	brackets := iter

	iter = iter.Right
	if iter == null return null

	resultToken := Token^

	if iter.GetValue() == "->" 
	{
		iter = iter.Right
		if iter == null return null
		resultToken = iter

		iter = iter.Right
		if iter == null return null
	}

	if iter.GetValue() != "{}" return null

	return new BoxFuncBody(Attr,brackets,null,resultToken,iter)
}

FuncAttributes := class
{
	IsMethod := bool
	IsVirtual := bool
	IsStatic := bool
		
	"=" := default
}

BoxBlock := class extend BoxUnit
{
	MetaId := int
	down := Token^
	exeLines := List.{BoxUnit^} //TODO : to hybrid
	this := !(Token^ bdy) -> void
	{
		down = bdy
		if DebugMode
			MetaId = GetNewId()
	}
	Work := virtual !() -> void
	{
		SyntaxCompress(down,PriorityData)

		iter := down.Down
		stackAtStart := UnitStack.Size()

		while iter != null
		{
			//TODO: check switch,if,while

			vr := TryParseVar(iter)

			if vr != null
			{
				pad := new BoxExeLineSimple(iter,vr)

				exeLines.Push(pad)
				UnitStack.Push(vr)
			}else{
				pad := new BoxExeLine()
				pad.DoLine(iter)

				exeLines.Push(pad)
			}
			
		
			iter = iter.Right
		}

		while UnitStack.Size() > stackAtStart
		{
			UnitStack.Pop()
		}
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{

		oldVal := GDebugScopeId
		GDebugScopeId = MetaId
		defer GDebugScopeId = oldVal

		exeLines[^].PrintCode(f)
	}
	PrintDebugMeta := virtual !(TIOStream^ f) -> void
	{
		f^ << "!" << MetaId << " = !DILexicalBlock(scope: !" 
		f^ << GDebugScopeId << ", file: !" << GDebugFileId
		if down?.Line != null
		{
			f^ << ", line: " << down.Line.Line
		}
		f^ << ")\n"

		oldVal := GDebugScopeId
		GDebugScopeId = MetaId
		defer GDebugScopeId = oldVal

		exeLines[^].PrintDebugMeta(f)
	}
}

labelIdIterator := 1
BoxLabel := class
{
	id := int
	this := !() -> void
	{
		id = labelIdIterator
		labelIdIterator += 1
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
		assert(false)
	}
}




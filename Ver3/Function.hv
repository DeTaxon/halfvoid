TryParseFunction := !(Token^ obj) -> BoxFunc^
{
	Attr := FuncAttributes

	if obj == null or obj.Down == null return null

	iter := obj.Down

	switch iter.GetValue() //TODO to cycle
	{
		case "virtual"
			Attr.IsVirtual = true
			iter = iter.Right
			if iter == null
				return null
		case void
	}

	if iter.GetValue() != "!"
		return null
	
	iter = iter.Right
	if iter == null return null

	if iter.GetValue() != "()"
		return null

	brackets := iter

	iter = iter.Right
	if iter == null return null

	resultToken := Token^

	if iter.GetValue() == "->" 
	{
		iter = iter.Right
		if iter == null return null
		resultToken = iter

		iter = iter.Right
		if iter == null return null
	}

	if iter.GetValue() != "{}" return null

	return new BoxFuncBody(Attr,brackets,null,resultToken,iter)
}

FuncAttributes := class
{
	IsMethod := bool
	IsVirtual := bool
	IsStatic := bool
		
	"=" := default
}

BoxFunc := class extend BoxUnit
{
	ItType := TypeFunc^
	ItTypeHatch := THatch

	GetType := !() -> TypeFunc^
	{
		ItTypeHatch.Await()
		return ItType
	}
	SetType := !(TypeFunc^ tp) -> void
	{
		ItType = tp
		ItTypeHatch.Emit()
	}
	WorkBody := virtual !() -> void
	{
	}
	SetName := virtual !(StringSpan nm) -> void
	{
	}
	WaitReadyState := virtual !() -> void
	{
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
	}
	PrintResultType := virtual !(TIOStream^ f) -> void
	{
		ItType.ResultType.PrintType(f) //TODO return class
	}
	PrintInputTypes := virtual !(TIOStream^ f) -> void
	{
		needComma := false
		for par,i : ItType.Params
		{
			if needComma
				f^ << ","
			needComma = true

			par.ItType.PrintType(f)
			f^ << " %par" << i
		}
		if ItType.IsVargs
		{
			if needComma
				f^ << ","
			f^ << "..."
		}
	}
	PrintName := virtual !(TIOStream^ f) -> void
	{
	}
}



BoxBlock := class extend BoxUnit
{
	down := Token^
	exeLines := List.{BoxUnit^} //TODO : to hybrid
	this := !(Token^ bdy) -> void
	{
		down = bdy
	}
	Work := virtual !() -> void
	{
		SyntaxCompress(down,PriorityData)

		iter := down.Down
		stackAtStart := UnitStack.Size()

		while iter != null
		{
			//TODO: check switch,if,while
			
			pad := new BoxExeLine
			pad.DoLine(iter)
			UnitStack.PushFront(pad)

			exeLines.Push(pad)
		
			iter = iter.Right
		}

		while UnitStack.Size() > stackAtStart
		{
			UnitStack.Pop()
		}
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
		exeLines[^].PrintCode(f)
	}
}

labelIdIterator := 1
BoxLabel := class
{
	id := int
	this := !() -> void
	{
		id = labelIdIterator
		labelIdIterator += 1
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
		assert(false)
	}
}




FuncInputParam := class extend ParamFunc
{
	this := !(Type^ pType,bool isRefIn,StringSpan pVal) -> void
	{
		parentFunc = CurrentFunc
		SetType(GetFuncType(0,null,null,pType,true,false))
		irName = pVal
		isRef = isRefIn
		if DebugMode
		{
			if isRef
				pType.MakeDebugRef()
		}
	}
	IsMem := virtual !() -> bool
	{
		return isRef
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
	}
	PrintResultObj := virtual !(TIOStream^ f, int resId, int debId) -> void
	{
		if GetType()->{TypeFunc^}.ResultType != GTypeVoid
		{
			f^ <<  irName
		}
	}
	PrintFuncCall := virtual !(TIOStream^ f,int resId,int debId,ExeDownList lst) -> void
	{
	}
	NotifyMemUse := virtual !() -> void
	{
		if isRef
			return void
		this&->SetType(FuncInputParamInMem)
	}
	PrintDebugMeta := virtual !(TIOStream^ f) -> void
	{
	}
	IsValue := virtual !() -> bool
	{
		return true
	}
}

FuncInputParamInMem := class extend FuncInputParam
{
	IsMem := virtual !() -> bool
	{
		return true
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{
		rType := GetType()->{TypeFunc^}.ResultType
		f^ << irName << "Unref = alloca "
		rType.PrintType(f)
		if isRef
			f^ << "*"
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
		
		f^ << "store "
		rType.PrintType(f)
		if isRef
			f^ << "*"
		f^ << " " << irName
		f^ << " , "
		rType.PrintType(f)
		f^ << "*"
		if isRef
			f^ << "*"
		f^ << " " << irName << "Unref"
		if DebugMode
			f^ << " , !dbg !" << GDebugLineCallId
		f^ << "\n"
	}
	PrintResultObj := virtual !(TIOStream^ f, int resId, int debId) -> void
	{
		if GetType()->{TypeFunc^}.ResultType != GTypeVoid
		{
			f^ << "" << irName << "Unref"
		}
	}
	IsValue := virtual !() -> bool
	{
		return false
	}
}

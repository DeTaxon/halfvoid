FuncLocalParam := class extend ParamFunc
{
	this := !(Type^ tp) -> void
	{
		parentFunc = CurrentFunc
		irName = GetLVarName(GetNewId())
		SetType(GetFuncType(0,null,null,tp,true,false))

		if tp is TypeClass
		{
			cls := tp->{TypeClass^}.ToClass
			cls.AwaitVTableStep()
			if cls.VTable.Size() != 0
			{
				dwns := CheckExeDownList
				fCall := new BoxFuncCall(this&,dwns)
				dwns.Push(fCall)
				cnsts := List.{BoxExeConstObj^}
				fnc := TemplateInternalInitClass.GetFunc(dwns,cnsts)
				Inits.Push(new BoxFuncCall(fnc,dwns))
			}
		}
	}
	IsMem := virtual !() -> bool
	{
		return true
	}
	PrintCode := virtual !(TIOStream^ f) -> void
	{

		pType := GetType()->{TypeFunc^}.ResultType

		f^ << "%" << irName << " = alloca "
		pType.PrintType(f)
		if DebugMode
			f^ << ", !dbg !" << GDebugLineCallId
		f^ << "\n"

		if IsInt(pType)
		{
			f^ << "store "
			pType.PrintType(f)
			f^ << " 0, "
			pType.PrintType(f)
			f^ << "* %" << irName
			if DebugMode
				f^ << ", !dbg !" << GDebugLineCallId
			f^ << "\n"
		}else //if pType is TypeClass
		{
			f^ << "%Void" << irName << " = bitcast "
			pType.PrintType(f)
			f^ << "* %" << irName << " to i8*"
			if DebugMode
				f^ << ", !dbg !" << GDebugLineCallId
			f^ << "\n"

			f^ << "call  void @llvm.memset.p0i8.i64(i8* %Void" << irName << ", i8 0, i64 " << pType.GetSize() << ",i1 false)"
			if DebugMode
				f^ << ", !dbg !" << GDebugLineCallId
			f^ << "\n"
		}
		Inits[^].PrintDefaultUse(f)
	}
	PrintResultObj := virtual !(TIOStream^ f, int resId, int debId) -> void
	{
		f^ << "%" << irName 
	}
	PrintDebugMeta := virtual !(TIOStream^ f) -> void
	{
	}
	PrintFuncCall := virtual !(TIOStream^ f,int resId,int debId,ExeDownList lst) -> void
	{
	}
}
GetLVarName := !(int x) -> StringSpan
{
	buff := char[100]
	sprintf(buff[0]&,"Var%i",x)
	result = GetConstString(buff[0]&)
}


CommonTreeNode := class .{@DATA}
{
	Left := CommonTreeNode.{DATA}^
	Right := CommonTreeNode.{DATA}^
	Up := CommonTreeNode.{DATA}^
	balance := s8
	data := DATA
	this := !() -> void
	{
		Left = null
		Right = null
		Up = null
		balance = 0
	}
}

CommonTree := class .{@DATA}
{
	Start := CommonTreeNode.{DATA}^
	
	FindNode := !(DATA dat) -> CommonTreeNode.{DATA}^
	{
		itr := Start
		while itr != null
		{
			stRes := itr.data <=> dat
			if stRes == 0 return itr
			if stRes < 0 	itr = itr.Right
			else 		itr = itr.Left
		}
		return itr
	}
	FindNodeByFirst := !(@SomeKey& key) -> CommonTreeNode.{DATA}^
	{
		itr := Start
		while itr != null
		{
			stRes := itr.data.first <=> key
			if stRes == 0 return itr
			if stRes < 0 	itr = itr.Right
			else 		itr = itr.Left
		}
		return itr
	}
	FindNode := !(!(DATA)&->int cmpFunc) -> CommonTreeNode.{DATA}^
	{
		itr := Start
		while itr != null
		{
			stRes := cmpFunc(itr.data)
			if stRes == 0 return itr
			if stRes < 0 	itr = itr.Right
			else 		itr = itr.Left
		}
		return itr
	}
	createNode := virtual !(CommonTreeNode.{DATA}^& res) -> void
	{
		res = new CommonTreeNode.{DATA}
	}
	RotateLeft := !(CommonTreeNode.{DATA}^ toRotate) -> void
	{
		dwNode := toRotate.Right
		toRotate.Right = dwNode.Left
		if toRotate.Right != null toRotate.Right.Up = toRotate
		dwNode.Up = toRotate.Up
		toRotate.Up = dwNode
		dwNode.Left = toRotate
		if dwNode.Up == null {
			Start = dwNode
		}else{
			if dwNode.Up.Left == toRotate dwNode.Up.Left = dwNode
			else dwNode.Up.Right = dwNode
		}
	}
	RotateRight := !(CommonTreeNode.{DATA}^ toRotate) -> void
	{
		dwNode := toRotate.Left
		toRotate.Left = dwNode.Right
		if toRotate.Left != null toRotate.Left.Up = toRotate
		dwNode.Up = toRotate.Up
		toRotate.Up = dwNode
		dwNode.Right = toRotate
		if dwNode.Up == null {
			Start = dwNode
		}else{
			if dwNode.Up.Left == toRotate dwNode.Up.Left = dwNode
			else dwNode.Up.Right = dwNode
		}
	}
	remNode := !(CommonTreeNode.{DATA}^ toRem) .{} -> void
	{
		delete toRem
	}
	Destroy := !() -> void
	{
		destroyNode(Start)

		Start = null
	}
	destroyNode := !(CommonTreeNode.{DATA}^ nd) -> void
	{
		if nd == null
			return void
		destroyNode(nd.Left)
		destroyNode(nd.Right)

		delete nd
	}
	//TODO Clear
}

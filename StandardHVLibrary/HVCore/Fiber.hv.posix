
fiber_context := class
{
	if #CPUArch() == "x86_64"
	{
		rip := void^
		rsp := void^
		rbx := void^
		rbp := void^
		r12 := void^
		r13 := void^
		r14 := void^
		r15 := void^

		//xmm6 := vec4f
		//xmm7 := vec4f
		//xmm8 := vec4f
		//xmm9 := vec4f
		//xmm10 := vec4f
		//xmm11 := vec4f
		//xmm12 := vec4f
		//xmm13 := vec4f
		//xmm14 := vec4f
		//xmm15 := vec4f

		"=" := default
	}else{
		buf := char[8192]
		"=" := default
	}
}
// fiber_context_arm64 := class
// {
// 	x19 := void^
// 	x20 := void^
// 	x21 := void^
// 	x22 := void^
// 	x23 := void^
// 	x24 := void^
// 	x25 := void^
// 	x26 := void^
// 	x27 := void^
// 	x28 := void^
//
// 	v8 := double
// 	v9 := double
// 	v10 := double
// 	v11 := double
// 	v12 := double
// 	v13 := double
// 	v14 := double
// 	v15 := double
//
// 	"=" := default
// }

get_context_x86_64 := !(fiber_context^ ctx) -> void
	#Attr("noinline")
{
	asm(
		"movq %0, %%rdi;"
		"call get_context_step%=;"
		"jmp get_context_end%=;"
		"get_context_step%=:"

		"movq (%%rsp),%%r8"
		"movq %%r8,8*0(%%rdi)"
		"leaq 8(%%rsp),%%r8"
		"movq %%r8,8*1(%%rdi)"

		"movq %%rbx,8*2(%%rdi)"
		"movq %%rbp,8*3(%%rdi)"
		"movq %%r12,8*4(%%rdi)"
		"movq %%r13,8*5(%%rdi)"
		"movq %%r14,8*6(%%rdi)"
		"movq %%r15,8*7(%%rdi)"

		"movq %%r15,8*7(%%rdi)"

		"xorl %%eax, %%eax"
		"ret;"
		"get_context_end%=:"
		:
		: "r"(ctx)
		: "%rdi"
	)
}
set_context_x86_64 := !(fiber_context^ ctx) -> void
	#Attr("noinline")
{
	asm(
		"movq 8*0(%%rdi),%%r8"
		"movq 8*1(%%rdi),%%rsp"

		"movq 8*2(%%rdi),%%rbx"
		"movq 8*3(%%rdi),%%rbp"
		"movq 8*4(%%rdi),%%r12"
		"movq 8*5(%%rdi),%%r13"
		"movq 8*6(%%rdi),%%r14"
		"movq 8*7(%%rdi),%%r15"

		"pushq %%r8"
		"xorl %%eax, %%eax"
		"ret;"
		:
		: "r"(ctx)
		: "%rdi","%r8"
	)
}


swap_context_x86_64 := !(fiber_context^ thisContext,fiber_context^ newContext) -> void
	#Attr("noinline")
{
	asm(
		"movq %0, %%rdi;"
		"movq %1, %%rsi;"
		"call swap_context_step%=;"
		"jmp swap_context_end%=;"
		"swap_context_step%=:"

		"movq (%%rsp),%%r8"
		"movq %%r8,8*0(%%rdi)"
		"leaq 8(%%rsp),%%r8"
		"movq %%r8,8*1(%%rdi)"

		"movq %%rbx,8*2(%%rdi)"
		"movq %%rbp,8*3(%%rdi)"
		"movq %%r12,8*4(%%rdi)"
		"movq %%r13,8*5(%%rdi)"
		"movq %%r14,8*6(%%rdi)"
		"movq %%r15,8*7(%%rdi)"


		"movq 8*0(%%rsi),%%r8"
		"movq 8*1(%%rsi),%%rsp"

		"movq 8*2(%%rsi),%%rbx"
		"movq 8*3(%%rsi),%%rbp"
		"movq 8*4(%%rsi),%%r12"
		"movq 8*5(%%rsi),%%r13"
		"movq 8*6(%%rsi),%%r14"
		"movq 8*7(%%rsi),%%r15"

		"pushq %%r8"
		"xorl %%eax, %%eax"
		"ret;"
		"swap_context_end%=:"
		:
		: "r"(thisContext) , "r"(newContext)
		: "%rdi","%r8", "%rsi"
	)
}

FiberGetContext := !(fiber_context^ ctx) -> void
{
	if #CPUArch() == "x86_64"
	{
		get_context_x86_64(ctx)
	}else{
		getcontext(ctx)
	}

}
FiberSetContext := !(fiber_context^ ctx) -> void
{
	if #CPUArch() == "x86_64"
	{
		set_context_x86_64(ctx)
	}else{
		setcontext(ctx)
	}

}
FiberSwapContext := !(fiber_context^ thisContext,fiber_context^ newContext) -> void
{
	if #CPUArch() == "x86_64"
	{
		swap_context_x86_64(thisContext,newContext)
	}else{
		swapcontext(thisContext,newContext)
	}
}


FiberCreate := !(fiber_context^ ctx,void^ fncPointer, void^ newStack, int stackSize) -> void
{
	FiberGetContext(ctx)
	if #CPUArch() == "x86_64"
	{
		ctx.rip = fncPointer
		ctx.rsp = newStack->{u8^}[stackSize - 128 - 8]& //128 - red zone
	}else{
		ctx->{u8^}[16]&->{void^^}^ = newStack
		ctx->{u8^}[32]&->{u64^}^ = stackSize
		makecontext(ctx,fncPointer,0)
	}
}

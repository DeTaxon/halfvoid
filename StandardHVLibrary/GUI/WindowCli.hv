WindowCli := class extend TGCObject
{
	mainGUIObject := GUIObject^

	ctx := DrawCliContext
	layoutContext := LayoutContext

	working := bool

	InitWindow := virtual !() -> GUIObject^
	{
		return new LayoutVertical(![])
	}

	Quit := !() -> void
	{
		working = false
	}
	Run := !() -> void
	{
		ctx.Window = initscr()
		raw()
		noecho()
		nonl()
		start_color()

		for it : 8
			init_pair(it + 1, it,COLOR_BLACK)

		ctx.Init()

		ctx.CurrentColor = Color("white") 

		mainGUIObject = InitWindow()
		
		layoutContext.w = getmaxx(ctx.Window)
		layoutContext.h = getmaxy(ctx.Window)
		layoutContext.DrawType = "Text"

		mainGUIObject.GetLayout(layoutContext&)
		mainGUIObject.SetLayout(layoutContext&,vec2f(layoutContext.w,layoutContext.h))
		
		working = true
		while working
		{
			draw()


			if #OS() == "Linux"
			{
				pollList := pollfd
				pollList.fd = STDIN_FILENO 
				pollList.events = POLLIN


				@work poll(pollList&,1,500)
				if pollList.revents != 0
				{
					c := wgetch(ctx.Window) //TODO more normal way to wait
					KeyPressed(c)
				}
			}
			if #OS() == "Windows"
			{
				assert(false)
			}

		}
		endwin()
	}
	draw := !() -> void
	{
		wclear(ctx.Window)
		mainGUIObject.DrawCli(ctx&,vec2i(0,0))
		refresh()
	}
	Refresh := !() -> void
	{
		draw()
	}
	KeyPressed := virtual !(int keyChar) -> void
	{
	}
}
DrawCliContext := class
{
	TerminalSize := vec2i
	Window := void^

	CurrentColor := Color
	Colors := Color[8]

	Init := !() -> void
	{
		Colors[0] = Color("black")	
		Colors[1] = Color("red")	
		Colors[2] = Color("green")
		Colors[3] = Color("yellow")
		Colors[4] = Color("blue")
		Colors[5] = Color("magenta")
		Colors[6] = Color("cyan")
		Colors[7] = Color("white")
	}
	DrawTextAt := !(int y, int x, Str text) -> void
	{
	}
	SetTextColor := !(Color color) -> void
	{
		dist := float[8]

		for i : 8
		{
			dist[i] += Abs(color.r - Colors[i].r)
			dist[i] += Abs(color.g - Colors[i].g)
			dist[i] += Abs(color.b - Colors[i].b)
		}

		minI := 0
		for i : 8
		{
			if dist[i] < dist[minI]
				minI = i
		}
		CurrentColor = color
		attron(COLOR_PAIR(minI + 1))
	}
}

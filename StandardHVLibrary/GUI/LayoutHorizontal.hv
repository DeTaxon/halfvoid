
LayoutHorizontal := class extend LayoutBase
{
	this := !(GUIObject^[] objs) -> void
	{
		super(objs)
	}
	GetLayout := virtual !(VkOverlayContext^ ctx) -> GUILayoutInfo
	{
		fullSizeCount = 0
		foundStaticW = false
		foundStaticH = false
		staticW = 0
		staticH = 0

		foundFullSizeW = false
		foundFullSizeH = false

		for it : objects
		{
			if it.0 is LayoutSpacer
			{
				itL := it.0.GetLayout(ctx)
				switch itL.1
				{	
					case 0
						foundFullSizeH = true
						fullSizeCount += 1
					case 1
						staticW += itL.0.x
						//foundStaticH = true
					case 2 //dynamic ??
				}
				continue
			}

			itL := it.0.GetLayout(ctx)
			switch itL.LayoutTypeW
			{
				case 0 //fullSize
					foundFullSizeW = true
					fullSizeCount += 1
				case 1 //static
					staticW = staticW + itL.Sizes.x
					foundStaticW = true
				case 2 //dynamic
			}
			switch itL.LayoutTypeH
			{
				case 0 //fullSize
					foundFullSizeH = true
				case 1 //static
					staticH = max(staticH,itL.Sizes.y)
					foundStaticH = true
				case 2 //dynamic
			}
		}

		if foundStaticW { result.Sizes.x = staticW }
		if foundStaticH { result.Sizes.y = staticH }

		if foundStaticW { result.LayoutTypeW = 1
		}else { result.LayoutTypeW = 0}

		if foundStaticH { result.LayoutTypeH = 1
		}else {result.LayoutTypeH = 0}
	}

	SetLayout := virtual !(VkOverlayContext^ ctx,vec2f setSize) -> void
	{
		maxHeight := setSize.y
		if foundStaticH
		{
			maxHeight = staticH
		}

		pOffset := float
		for it : objects
		{
			if it.0 is LayoutSpacer
			{
				itL := it.0.GetLayout(ctx)
				switch itL.LayoutTypeW
				{
					case 0
						pOffset += (setSize.x - staticW) / fullSizeCount
					case 1
						pOffset += itL.Sizes.x
				}
				continue
			}
			itL := it.0.GetLayout(ctx)
			newSize := vec2f
			switch itL.LayoutTypeW
			{
				case 0
					newSize = setSize
					newSize.x = (setSize.x - staticW) / fullSizeCount
					it.0.SetLayout(ctx,newSize)
					it.1.y = 0
					it.1.x = pOffset
					it.2 = newSize
				case 1
					switch itL.LayoutTypeH
					{
						case 1
							newSize.y = itL.Sizes.y
						case 0
							newSize.y = maxHeight
					}
					newSize.x = itL.Sizes.x
					it.0.SetLayout(ctx,newSize)
					it.1.y = 0
					it.1.x = pOffset
					it.2 = newSize
				case 2
					assert(false)
			}
			pOffset += newSize.x
		}
	}
	PushDraw := virtual !(VkOverlayContext^ ctx,VkCommandBuffer cmdB,vec2f offset) -> void
	{
		for it : objects
		{
			if it.0 is LayoutSpacer
				continue

			newOffset := offset + it.1
			it.0.PushDraw(ctx,cmdB,offset + it.1)
		}
	}
}

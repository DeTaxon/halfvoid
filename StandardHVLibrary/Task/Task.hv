taskIdIterator := 1
taskNames := AVLMap.{int,Tuple.{char[20],bool}}
taskNamesFree := 0

Task := class extend TGCObject
{
	taskLocalData := internalHVTaskLocalTuple
	itTaskWorker := TaskWorker^

	taskLambda := !()&->void
	finishedWork := THatch
	taskId := int

	longJumpBuf := void^[5]

	this := !(!()& -> void cb) -> void
	{
		taskLambda = cb
	}
	startTaskThread := !() -> void
	{
		if #Var("Tracy")
		{

			if taskNamesFree != 0
			{
				taskNamesFree -= 1
				for it, ind : taskNames
				{
					if it.1
						continue
					taskId = ind
					break
				}
			}else{
				taskIdIterator += 1
				taskId = taskIdIterator
			}
			taskData := ref taskNames[taskId]
			sprintf(taskNames[taskId].0[0]&,"Task%i\n",taskId)
			taskData.1 = true
		}
		TaskCheckMutex.Unlock()
		firstStartOS()
		assert(taskLambda != null)
		taskLambda()
		TaskCheckMutex.Lock()
		if #Var("Tracy")
		{
			if taskId != 0
			{
				taskNamesFree += 1
				taskNames[taskId].1 = false
				taskId = 0
			}
		}
		CurrentThread.DeleteTasks.Push(this&)
		finishedWork.EmitInner()
		taskLambda = null
		CurrentThread.ThreadMainTask.switchToTask()
	}


	resume := !() -> void
	{
		itTaskWorker.PendingTasks.Push(this&)
	}

	Destroy := virtual !() -> void
	{

		OSDestroy()
		taskLambda = null

		for fld : this.taskLocalData->AllFields
		{
			if fld->TypeGroup == "Pointer"
			{
				fld = null
			}
		}
	}
	AwaitCompletion := !() -> void
	{
		finishedWork.Await()
	}
}

TaskStackUsageInfo := class
{
	TotalSize := size_t
	UsedSize := size_t

	Used := !() -> size_t
	{
		return UsedSize
	}
	Free := !() -> size_t
	{
		return TotalSize - UsedSize
	}
	ProcUsage := !() -> double
	{
		if TotalSize == 0
			return 0.0
		return this.Used()->{double} / TotalSize->{double}
	}
	ProcFree := !() -> double
	{
		if TotalSize == 0
			return 0.0
		return this.Free()->{double} / TotalSize->{double}
	}
}



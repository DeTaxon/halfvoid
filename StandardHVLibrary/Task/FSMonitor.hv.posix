

IN_CLOSE := 24
IN_OPEN := 32
IN_MODIFY := 2


wd_struct := class
{
	wd := s32
	mask := u32
	cookie := u32

	len := u32
	name := char[1]
}


//TODO
//Check for repeats
FIONREAD := 21531
inotify_wrapper_fd := class
{
	fd := int

	fileNodes := AVLMap.{int,inotify_wrapper_node}

	Init := !() -> void
	{
	}

	MonitorFolder := !(StringView folderPath,bool recursive,!(FSObject^)& -> void cb) -> TGCObject^
	{
		@once {
			this.fd = inotify_init1(2048)

			TSpawnTask(() ==> [this&]{
				OSAsyncReadNonStop(this.fd,(blb) ==> [this&]{

					ptr := blb.GetPointer()->{wd_struct^}
					fileName := StringSpan(ptr.name[0]&,ptr.len)

					node := ref fileNodes[ptr.wd]
					sBuf := TEchoStream
					sBuf << node.objectName << "/" << fileName

					fullPath := sBuf.GetString()

					newFile := new File()
					newFile.absPath = fullPath
					newFile.objName = fileName.GetString()

					if node.nodeWork.isPaused
					{
						node.nodeWork.monitorTask.resume()
						node.nodeWork.isPaused = false
						node.nodeWork.changedObject = newFile
					}
				})
			})
		}


		thisWork := NodeWork
		thisWork.monitorTask = CurrentTask
		TLock(this&)

		wd := inotify_add_watch(this.fd,folderPath.GetString().Str(),IN_MODIFY)
		newNode := ref fileNodes[wd]
		newNode.objectName = folderPath.GetString()
		newNode.nodeWork = thisWork&

		if recursive
		{
			FSReadFolder(folderPath,true, (x) ==> {
				if x is not in Folder
					return void
				wd := inotify_add_watch(this.fd,x.GetPath().Str(),IN_MODIFY)
				newNode := ref fileNodes[wd]
				newNode.objectName = x.GetPath()
				newNode.nodeWork = thisWork&
			})
		}

		TUnlock(this&)

		thrd := CurrentThread
		//TODO Cancalable
		while true
		{
			TaskCheckMutex.Lock()
			thisWork.isPaused = true
			thrd.switchToMain()
			TaskCheckMutex.Unlock()

			if thisWork.changedObject != null
			{
				cb(thisWork.changedObject)
				thisWork.changedObject = null
			}
		}
	}
}
inotify_wrapper_node := class
{
	objectName := String^
	nodeWork := NodeWork^
}

NodeWork := class
{
	monitorTask := Task^
	changedObject := FSObject^ //TODO multiple objects
	isPaused := bool
}

iw_fd := inotify_wrapper_fd
//TODO add destroy
initMonitorFolder := !() -> void
{
	iw_fd.Init()
}


//#MarkFunction("JITPass",AsyncMonitorFolderNonStop)
AsyncMonitorFolderNonStop := !(StringView folderPath,bool recursive,!(FSObject^)& -> void cb) -> TGCObject^
{
	#MarkFunction("CallAtStart",initMonitorFolder)

	return iw_fd.MonitorFolder(folderPath,recursive,cb)
}


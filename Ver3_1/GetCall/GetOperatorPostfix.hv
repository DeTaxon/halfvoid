TryGetPostfixOperator := !(Token^ itm) -> BoxExeObj^
{
	if itm == null return null
	if itm.Down == null return null
	if itm.Down.Right == null return null
	if itm.Down.Right.Right != null return null

	oper := itm.Down.Right.GetValue()

	if oper == "?"
	{
		//TODO case x[10]?
		cl := TryGetExeCall(itm.Down)
		assert(cl != null)

		if not (cl.GetType() is in TypePoint)
		{
			itm.Down.Right.EmitError("Left of ? operator must be pointer")
		}

		return new BoxQuestionPointer(cl)
	}
	if oper == "[]"
	{
		Objs := CheckExeDownList
		
		dObj := TryGetExeCall(itm.Down)
		if dObj == null
			return null
		Objs.Push(dObj)
		SyntaxCompress(itm.Down.Right,PriorityData)
		CheckFuncCallBrackets(itm.Down.Right,Objs)

		cnsts := List.{BoxExeConstObj^}
		return CreateFuncCallOperator(StringSpan("[]"),Objs,cnsts)

	}
	if oper == "--" or oper == "++"
	{
		Objs2 := CheckExeDownList
		dObj := TryGetExeCall(itm.Down)
		if dObj == null
			return null
		Objs2.Push(dObj)

		return CreateFuncCall(oper,Objs2)
	}
	return null
}
